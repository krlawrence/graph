// vim: set tw=85 cc=+1 wrap spell redrawtime=20000:
[[msc]]
== MISCELLANEOUS QUERIES AND THEIR RESULTS

In this chapter you will find more Gremlin queries that operate on the 'air-routes'
graph. All of these queries build upon the topics  covered in the prior sections.
In this section we have included lots of examples of the output returned by running
queries. In cases where the output is rather lengthy, we have either truncated it or
laid it out in columns to make it easier to read and to save space. It is our hope
also that from reading the examples in this section that you will get a sense of how
good data modeled as a graph can be when used for analysis. we also think that the
queries in this section show that you can achieve useful results from a graph using
nothing more than some OLTP style queries and a TinkerGraph. This is actually a great
example of an ideal use case for TinkerGraph. Even if you have your main data in a
massive hosted graph, extracting parts of it and doing analysis locally using
TinkerGraph is a technique that can make you very productive.

[[countmore]]
=== Counting more things

To get things started, let's look at a few more examples that basically just count
occurrences and distributions of things but are a little more complex than the
examples we looked at earlier in the book.

[[noairports]]
==== Which countries have no airports?

This first query looks for any 'country' vertices that have no outgoing edges. This
indicates that there are no airports in the graph for those countries.

[source,groovy]
----
// Are there any countries that have no airports?
g.V().hasLabel('country').not(out()).values('desc')
----

So it seems there are five countries for which no airports were found.

[source,groovy]
----
Andorra
Liechtenstein
Monaco
Pitcairn
San Marino
----

The previous query is a slightly shorter form of the two queries below which would
both yield the same results. Note the use of the '"__"' prefix in front of the 'not'
step in the second query. This is because 'not' is a reserved word in Groovy and has
to be prefixed in this way when not directly connected to a prior step by a dot.

[source,groovy]
----
// Are there any countries that have no airports?
g.V().hasLabel('country').where(out().count().is(0)).values('desc')

g.V().hasLabel('country').where(__.not(out())).values('desc')

----

[[noroutes]]
==== Which airports have no routes?

There are a few airports in the graph that currently have no commercial routes. This
is either because they used to have service and it was discontinued, or they are new
airports still awaiting service to start. We can write a query to easily find these
"orphan" airports.

In more recent updates of the data set, some of these airports
do now have commercial airline service.


[source,groovy]
----
g.V().hasLabel('airport').not(bothE('route')).values('code').fold()
----

When we run the query, you can see we find quite a few orphan airport nodes that have
no outgoing or incoming routes.

[source,groovy]
----
[TXL,SXF,ILG,TWB,TUA,BVS,KGG,RIG,INT,APA,BWU,BID,NBW,SFH,CVT,AFW,PSY,EKA,ISL,APK,CRC,GYZ,HFN,HZK,NFO,SFD,VCV,YEI]
----

[[runwaydist]]
==== What is the distribution of runways?

We can easily count the distribution per airport of runways. We can observe from the
results of running the query that the vast majority of airports in the graph have
either one or two runways.

[source,groovy]
----
// What is the distribution of runways in the graph
g.V().hasLabel('airport').groupCount().by('runways')

[1:2429,2:775,3:227,4:53,5:14,6:4,7:2]
----

[[mostroutes]]
==== Airports with the most routes

This next query finds all airports that have more than 180 outgoing routes and
returns their IATA codes.

[source,groovy]
----
// Airports with more than 180 outgoing routes
g.V().hasLabel('airport').
      where(out('route').count().is(gt(180))).values('code').fold()

[ATL,DFW,IAH,JFK,LAX,MIA,ORD,DEN,EWR,YYZ,LHR,LGW,CDG,FRA,DXB,DUB,PEK,PVG,FCO,AMS,BCN,MAD,VIE,BRU,MUC,MAN,STN,DME,SVO,DOH,IST,CPH,CLT,DUS,BER]
----

We could improve our query a bit to include the IATA code and the exact number of
outgoing routes in the returned result. There are a few different ways that we could
do this. One way that is quite convenient is to use 'group'.

[source,groovy]
----
// Same basic query but return the airport code and the route count
g.V().hasLabel('airport').
      where(out('route').count().is(gt(180))).
      group().by('code').by(out().count())
----

We have laid the results out in a grid to make them easier to read.

[source,groovy]
----
[ORD:265,PVG:213,LAX:214,BRU:194,
 CDG:293,CLT:184,STN:211,SVO:181,
 JFK:204,DUB:185,DFW:253,LHR:221,
 MUC:270,DOH:187,DME:232,EWR:203,
 DUS:199,MIA:196,AMS:283,IST:309,
 DEN:217,BCN:203,BER:202,DXB:248,
 IAH:199,MAD:206,FCO:201,VIE:206,
 FRA:310,PEK:248,ATL:242,CPH:194,
 YYZ:195,MAN:216,LGW:232]
----

We can add one further refinement to the query. This time the results are ordered by
the number or routes in descending order. Note the use of 'local' to make sure that
'order' is applied to the contents of the collection that was generated by the
'group' step.

[source,groovy]
----
// Same query with ordered results
g.V().hasLabel('airport').
      where(out('route').count().is(gt(180))).
      group().by('code').by(out().count()).
      order(local).by(values,desc)
----

We have again laid the results out in a grid.

[source,groovy]
----
[FRA:310, IST:309, CDG:293, AMS:283,
 MUC:270, ORD:265, DFW:253, DXB:248,
 PEK:248, ATL:242, DME:232, LGW:232,
 LHR:221, DEN:217, MAN:216, LAX:214,
 PVG:213, STN:211, MAD:206, VIE:206,
 JFK:204, EWR:203, BCN:203, BER:202,
 FCO:201, DUS:199, IAH:199, MIA:196,
 YYZ:195, BRU:194, CPH:194, DOH:187,
 DUB:185, CLT:184, SVO:181]
----

As we mentioned earlier in the book, the number of incoming and outgoing routes for
any given airport will not always be the same due to how airlines operate their
flight routing. The query below looks for any airports that have more than 400 total
routes (inbound and outbound).

[source,groovy]
----
// Airports with more than 400 total routes
g.V().hasLabel('airport').
      where(both('route').count().is(gt(400))).
      values('code').fold()

[ATL,DFW,JFK,LAX,ORD,DEN,EWR,LHR,LGW,CDG,FRA,DXB,PEK,PVG,FCO,AMS,BCN,MAD,VIE,MUC,MAN,STN,DME,IST,BER]
----

[[oneroute]]
==== Airports with just one route

There are, perhaps surprisingly, a large number of airports that only have one route.
The query below will figure out just how many fall into that category.

[source,groovy]
----
// How many airports have only one route?
g.V().hasLabel('airport').
  where(out().count().is(eq(1))).count()

777
----

[[onerunway]]
==== Single runway airports with the most routes

It is interesting to look at how busy some single runway airports are. The query
below looks for the ten airports with just one runway that have the most outgoing
routes. You will notice that we have included London Gatwick in the query using an
'or' step. This is because while technically Gatwick is listed in the graph as an
airport with two runways, in practice the second runway is primarily used as a
taxiway and reserved for emergency use only. Therefore, Gatwick is really a single
runway airport. This also makes the query a bit more interesting!

[source,groovy]
----
g.V().or(has('airport','runways',1),has('code','LGW')).
      order().by(out().count(),desc).limit(10).
      project('apt','city','routes').
      by('code').by('city').by(out().count())
----

When we run the query here are the results we get back. One interesting observation
is that three of the top five busiest single runway airports are in England.

[source,groovy]
----
[apt:LGW,city:London,routes:232]
[apt:STN,city:London,routes:211]
[apt:LIS,city:Lisbon,routes:139]
[apt:CTU,city:Chengdu,routes:139]
[apt:LTN,city:London,routes:130]
[apt:BHX,city:Birmingham,routes:130]
[apt:SZX,city:Shenzhen,routes:129]
[apt:CKG,city:Chongqing,routes:122]
[apt:STR,city:Stuttgart,routes:121]
[apt:CRL,city:Brussels,routes:117]
----

[[runwaycountproject]]
==== Another way of counting runways

Let's assume we wanted to count the runways in the graph and for each number of
runways produce a simple map result where the runway number and total count of
airports having that number of runways  are each meaningfully labeled. One way we
could do that is to use a 'groupCount' step to calculate the distribution of runways
and then use a 'project' step to produce the nicely labeled result. That is what the
query below does. Notice that an 'unfold' step is used so that the results of
the 'groupCount' which itself produces a map, can be further processed.

[source,groovy]
----
g.V().hasLabel('airport').
      groupCount().by('runways').
      unfold().
      project('runways','count').by(keys).by(values)
----

When run we get a nice map back showing us the number of runways and for each the
total count. Each value has a meaningful key name of 'runways' and 'count'
respectively.

[source,groovy]
----
[runways:1,count:2429]
[runways:2,count:775]
[runways:3,count:227]
[runways:4,count:53]
[runways:5,count:14]
[runways:6,count:4]
[runways:7,count:2]
----

Notice that if the unfold step had not been used, a very different result would have
been generated.

[source,groovy]
----
g.V().hasLabel('airport').
      groupCount().by('runways').
      project('runways','count').by(keys).by(values)

[runways:[1,2,3,4,5,6,7],count:[2429,775,227,53,14,4,2]]
----
[[canadamost]]
==== Airports with the most routes in Canada

The following query finds the top 10 airports in Canada sorted by descending number
of outgoing routes. Just for fun, this time we used the 'index' method to include a
one based index as part of the results.

[source,groovy]
----
g.V().has('country','code','CA').out().
      order().by(out().count(),desc).limit(10).
      project('apt','city','routes').
      by('code').by('city').by(out().count()).indexed(1)
----

Here are the results of running the query along with the index that we added.

[source,groovy]
----
[1,[apt:YYZ,city:Toronto,routes:195]]
[2,[apt:YUL,city:Montreal,routes:123]]
[3,[apt:YVR,city:Vancouver,routes:106]]
[4,[apt:YYC,city:Calgary,routes:75]]
[5,[apt:YEG,city:Edmonton,routes:48]]
[6,[apt:YHZ,city:Halifax,routes:45]]
[7,[apt:YWG,city:Winnipeg,routes:38]]
[8,[apt:YOW,city:Ottawa,routes:36]]
[9,[apt:YZF,city:Yellowknife,routes:21]]
[10,[apt:YQB,city:Quebec City,routes:20]]
----

[[ukdist]]
==== Distribution of UK airports

How many airports are there in each of the UK regions of England, Scotland,
Wales and Northern Ireland?

[source,groovy]
----
g.V().has('country','code','UK').out('contains').groupCount().by('region')

[GB-ENG:27,GB-WLS:3,GB-NIR:3,GB-SCT:25]
----

[[countrydist]]
==== Distribution of airports by country

This query uses 'groupCount' to produce a map of key value pairs where the key is the
two character ISO country code and the value is the number of airports that country
has.

[source,groovy]
----
// How many airports does each country have in the graph?
g.V().hasLabel('airport').
      groupCount().by('country')
----

When run the query produces quite a lot of output. As the values are not sorted it is
hard to find the countries with the most airports. Note that the 'groupCount' step
does not include countries that had no airports. In other words if the count is zero
the country was skipped.

[source,groovy]
----
[PR:7,PT:19,PW:1,PY:2,QA:1,AE:10,AF:5,AG:1,AI:1,AL:1,AM:2,AO:14,AR:38,AS:1,AT:6,RE:2,AU:132,AW:1,AZ:5,RO:14,BA:4,BB:1,RS:3,BD:7,BE:5,RU:129,BF:2,BG:4,RW:2,BH:1,BI:1,BJ:1,BL:1,BM:1,BN:1,BO:15,SA:26,BQ:3,SB:17,BR:117,SC:2,BS:18,SD:5,SE:39,BT:1,SG:2,BW:4,SH:2,SI:2,BY:3,BZ:13,SK:2,SL:1,SN:4,SO:6,CA:205,SR:1,SS:1,CC:1,CD:11,ST:1,SV:1,CF:1,CG:3,CH:6,SX:1,CI:2,SY:2,SZ:1,CK:6,CL:17,CM:5,CN:217,CO:51,CR:13,TC:4,TD:5,CU:12,CV:7,TG:1,TH:33,CW:1,CX:1,CY:3,TJ:4,CZ:5,TL:1,TM:1,TN:8,TO:5,TR:52,TT:2,DE:34,TV:1,TW:9,TZ:11,DJ:1,DK:8,DM:1,DO:7,UA:15,UG:4,UK:58,DZ:30,US:586,EC:15,EE:3,EG:11,EH:2,UY:2,UZ:11,ER:1,VC:1,ES:43,ET:16,VE:27,VG:2,VI:2,VN:22,VU:26,FI:20,FJ:10,FK:2,FM:4,FO:1,FR:59,WF:2,GA:2,WS:1,GD:1,GE:3,GF:1,GG:2,GH:6,GI:1,GL:14,GM:1,GN:1,GP:1,GQ:2,GR:39,GT:2,GU:1,GW:1,GY:2,HK:1,HN:6,HR:8,HT:2,YE:9,HU:2,ID:70,YT:1,IE:7,IL:6,IM:1,IN:77,ZA:20,IQ:6,IR:45,IS:7,IT:38,ZM:9,JE:1,ZW:3,JM:2,JO:2,JP:65,KE:14,KG:2,KH:3,KI:2,KM:1,KN:2,KP:1,KR:15,KS:1,KW:1,KY:3,KZ:21,LA:8,LB:1,LC:2,LK:6,LR:2,LS:1,LT:3,LU:1,LV:1,LY:11,MA:16,MD:1,ME:2,MF:1,MG:13,MH:2,MK:2,ML:1,MM:17,MN:10,MO:1,MP:2,MQ:1,MR:3,MS:1,MT:1,MU:2,MV:8,MW:7,MX:60,MY:35,MZ:11,NA:5,NC:1,NE:1,NF:1,NG:19,NI:1,NL:5,NO:49,NP:11,NR:1,NZ:25,OM:4,PA:5,PE:22,PF:39,PG:26,PH:40,PK:21,PL:13,PM:1]
----


If we wanted to sort the list in descending order using the numeric values we could
adjust the query as follows. Once again note the use of 'local' to specify how the
ordering is applied.

[source,groovy]
----
g.V().hasLabel('airport').
      groupCount().by('country').
      order(local).by(values,desc)
----

This time it is much easier to see which countries have the most airports.

[source,groovy]
----
[US:586,CN:217,CA:205,AU:132,RU:129,BR:117,IN:77,ID:70,JP:65,MX:60,FR:59,UK:58,TR:52,CO:51,NO:49,IR:45,ES:43,PH:40,SE:39,GR:39,PF:39,AR:38,IT:38,MY:35,DE:34,TH:33,DZ:30,VE:27,SA:26,VU:26,PG:26,NZ:25,VN:22,PE:22,KZ:21,PK:21,FI:20,ZA:20,PT:19,NG:19,BS:18,SB:17,CL:17,MM:17,ET:16,MA:16,BO:15,UA:15,EC:15,KR:15,AO:14,RO:14,GL:14,KE:14,BZ:13,CR:13,MG:13,PL:13,CU:12,CD:11,TZ:11,EG:11,UZ:11,LY:11,MZ:11,NP:11,AE:10,FJ:10,MN:10,TW:9,YE:9,ZM:9,TN:8,DK:8,HR:8,LA:8,MV:8,PR:7,BD:7,CV:7,DO:7,IE:7,IS:7,MW:7,AT:6,SO:6,CH:6,CK:6,GH:6,HN:6,IL:6,IQ:6,LK:6,AF:5,AZ:5,BE:5,SD:5,CM:5,TD:5,CZ:5,TO:5,NA:5,NL:5,PA:5,BA:4,BG:4,BW:4,SN:4,TC:4,TJ:4,UG:4,FM:4,OM:4,RS:3,BQ:3,BY:3,CG:3,CY:3,EE:3,GE:3,ZW:3,KH:3,KY:3,LT:3,MR:3,PY:2,AM:2,RE:2,BF:2,RW:2,SC:2,SG:2,SH:2,SI:2,SK:2,CI:2,SY:2,TT:2,EH:2,UY:2,VG:2,VI:2,FK:2,WF:2,GA:2,GG:2,GQ:2,GT:2,GY:2,HT:2,HU:2,JM:2,JO:2,KG:2,KI:2,KN:2,LC:2,LR:2,ME:2,MH:2,MK:2,MP:2,MU:2,PW:1,QA:1,AG:1,AI:1,AL:1,AS:1,AW:1,BB:1,BH:1,BI:1,BJ:1,BL:1,BM:1,BN:1,BT:1,SL:1,SR:1,SS:1,CC:1,ST:1,SV:1,CF:1,SX:1,SZ:1,TG:1,CW:1,CX:1,TL:1,TM:1,TV:1,DJ:1,DM:1,ER:1,VC:1,FO:1,WS:1,GD:1,GF:1,GI:1,GM:1,GN:1,GP:1,GU:1,GW:1,HK:1,YT:1,IM:1,JE:1,KM:1,KP:1,KS:1,KW:1,LB:1,LS:1,LU:1,LV:1,MD:1,MF:1,ML:1,MO:1,MQ:1,MS:1,MT:1,NC:1,NE:1,NF:1,NI:1,NR:1,PM:1]
----

If we wanted to sort by the country code, the 'key' in other words, we could change
the query accordingly. In this case we will use 'by(keys,asc)' to get a sort in
ascending order. If we wanted to sort in descending order by key we could use
'by(keys,desc)' instead.

[source,groovy]
----
g.V().hasLabel('airport').
      groupCount().by('country').
      order(local).by(keys,asc)
----

This time the results are now sorted using the country codes in ascending
alphabetical order.

[source,groovy]
----
[AE:10,AF:5,AG:1,AI:1,AL:1,AM:2,AO:14,AR:38,AS:1,AT:6,AU:132,AW:1,AZ:5,BA:4,BB:1,BD:7,BE:5,BF:2,BG:4,BH:1,BI:1,BJ:1,BL:1,BM:1,BN:1,BO:15,BQ:3,BR:117,BS:18,BT:1,BW:4,BY:3,BZ:13,CA:205,CC:1,CD:11,CF:1,CG:3,CH:6,CI:2,CK:6,CL:17,CM:5,CN:217,CO:51,CR:13,CU:12,CV:7,CW:1,CX:1,CY:3,CZ:5,DE:34,DJ:1,DK:8,DM:1,DO:7,DZ:30,EC:15,EE:3,EG:11,EH:2,ER:1,ES:43,ET:16,FI:20,FJ:10,FK:2,FM:4,FO:1,FR:59,GA:2,GD:1,GE:3,GF:1,GG:2,GH:6,GI:1,GL:14,GM:1,GN:1,GP:1,GQ:2,GR:39,GT:2,GU:1,GW:1,GY:2,HK:1,HN:6,HR:8,HT:2,HU:2,ID:70,IE:7,IL:6,IM:1,IN:77,IQ:6,IR:45,IS:7,IT:38,JE:1,JM:2,JO:2,JP:65,KE:14,KG:2,KH:3,KI:2,KM:1,KN:2,KP:1,KR:15,KS:1,KW:1,KY:3,KZ:21,LA:8,LB:1,LC:2,LK:6,LR:2,LS:1,LT:3,LU:1,LV:1,LY:11,MA:16,MD:1,ME:2,MF:1,MG:13,MH:2,MK:2,ML:1,MM:17,MN:10,MO:1,MP:2,MQ:1,MR:3,MS:1,MT:1,MU:2,MV:8,MW:7,MX:60,MY:35,MZ:11,NA:5,NC:1,NE:1,NF:1,NG:19,NI:1,NL:5,NO:49,NP:11,NR:1,NZ:25,OM:4,PA:5,PE:22,PF:39,PG:26,PH:40,PK:21,PL:13,PM:1,PR:7,PT:19,PW:1,PY:2,QA:1,RE:2,RO:14,RS:3,RU:129,RW:2,SA:26,SB:17,SC:2,SD:5,SE:39,SG:2,SH:2,SI:2,SK:2,SL:1,SN:4,SO:6,SR:1,SS:1,ST:1,SV:1,SX:1,SY:2,SZ:1,TC:4,TD:5,TG:1,TH:33,TJ:4,TL:1,TM:1,TN:8,TO:5,TR:52,TT:2,TV:1,TW:9,TZ:11,UA:15,UG:4,UK:58,US:586,UY:2,UZ:11,VC:1,VE:27,VG:2,VI:2,VN:22,VU:26,WF:2,WS:1,YE:9,YT:1,ZA:20,ZM:9,ZW:3]
----

Note that we can use 'select' to only return one or more of the full set of key/value
pairs returned. Here is an example of doing just that.

[source,groovy]
----
// Only return the values for Germany, China, Holland and the US.
g.V().hasLabel('airport').
      groupCount().by('country').
      select('DE','CN','NL','US')

[DE:34,CN:217,NL:5,US:586]
----

Because the 'air-routes' graph also has country specific vertices, we could chose to
write the previous queries a different way. We could start by finding country
vertices and then see how many airport vertices each one is connected to. In this
instance, because a 'group' step is being used, countries with no airports will be
included.

[source,groovy]
----
// Another way to ask the question above, this time by counting the
// edges (out degree) from each country

g.V().hasLabel('country').
      group().by('code').by(outE().count()).
      order(local).by(values,desc)
----

This time the countries with no airports *are* included in the results.

[source,groovy]
----
[US:586,CN:217,CA:205,AU:132,RU:129,BR:117,IN:77,ID:70,JP:65,MX:60,FR:59,UK:58,TR:52,CO:51,NO:49,IR:45,ES:43,PH:40,SE:39,GR:39,PF:39,AR:38,IT:38,MY:35,DE:34,TH:33,DZ:30,VE:27,SA:26,VU:26,PG:26,NZ:25,VN:22,PE:22,KZ:21,PK:21,FI:20,ZA:20,PT:19,NG:19,BS:18,SB:17,CL:17,MM:17,ET:16,MA:16,BO:15,UA:15,EC:15,KR:15,AO:14,RO:14,GL:14,KE:14,BZ:13,CR:13,MG:13,PL:13,CU:12,CD:11,TZ:11,EG:11,UZ:11,LY:11,MZ:11,NP:11,AE:10,FJ:10,MN:10,TW:9,YE:9,ZM:9,TN:8,DK:8,HR:8,LA:8,MV:8,PR:7,BD:7,CV:7,DO:7,IE:7,IS:7,MW:7,AT:6,SO:6,CH:6,CK:6,GH:6,HN:6,IL:6,IQ:6,LK:6,AF:5,AZ:5,BE:5,SD:5,CM:5,TD:5,CZ:5,TO:5,NA:5,NL:5,PA:5,BA:4,BG:4,BW:4,SN:4,TC:4,TJ:4,UG:4,FM:4,OM:4,RS:3,BQ:3,BY:3,CG:3,CY:3,EE:3,GE:3,ZW:3,KH:3,KY:3,LT:3,MR:3,PY:2,AM:2,RE:2,BF:2,RW:2,SC:2,SG:2,SH:2,SI:2,SK:2,CI:2,SY:2,TT:2,EH:2,UY:2,VG:2,VI:2,FK:2,WF:2,GA:2,GG:2,GQ:2,GT:2,GY:2,HT:2,HU:2,JM:2,JO:2,KG:2,KI:2,KN:2,LC:2,LR:2,ME:2,MH:2,MK:2,MP:2,MU:2,PW:1,QA:1,AG:1,AI:1,AL:1,AS:1,AW:1,BB:1,BH:1,BI:1,BJ:1,BL:1,BM:1,BN:1,BT:1,SL:1,SR:1,CC:1,SS:1,ST:1,CF:1,SV:1,SX:1,SZ:1,TG:1,CW:1,CX:1,TL:1,TM:1,TV:1,DJ:1,DM:1,ER:1,VC:1,FO:1,WS:1,GD:1,GF:1,GI:1,GM:1,GN:1,GP:1,GU:1,GW:1,HK:1,YT:1,IM:1,JE:1,KM:1,KP:1,KS:1,KW:1,LB:1,LS:1,LU:1,LV:1,MD:1,MF:1,ML:1,MO:1,MQ:1,MS:1,MT:1,NC:1,NE:1,NF:1,NI:1,NR:1,PM:1,AD:0,SM:0,LI:0,MC:0,PN:0]
----

If we only wanted to see the last few of the sorted results we could add a 'tail'
step with 'local' scope to the query.

[source,groovy]
----
g.V().hasLabel('country').
      group().by('code').by(outE().count()).
      order(local).by(values,desc).
      tail(local,20)

[LU:1,LV:1,MD:1,MF:1,ML:1,MO:1,MQ:1,MS:1,MT:1,NC:1,NE:1,NF:1,NI:1,NR:1,PM:1,AD:0,SM:0,LI:0,MC:0,PN:0]
----

[[continentdist]]
Distribution of airports by continent
We can use a similar query to the one above to find out how many airports are located
in each of the seven continents. As you can see from the output, a key/value map is 
again returned where the key is the continent code and the value is the number of 
airports in that continent. Note that currently  there are no airports with regular 
scheduled service in Antarctica!

[source,groovy]
----
// How many airports are there in each continent?
g.V().hasLabel('continent').group().by('code').by(out().count())

[EU:605,AS:971,NA:989,OC:305,AF:321,AN:0,SA:313]
----

==== Distribution of routes per airport

We have already examined various ways to calculate the distribution of routes in the
graph. The following query will, for each airport, return a key value pair where the
key is the airport code and the value is the number of outgoing routes from that
airport. Because there are over 3,000 airports in the graph, this query will produce
a large results. We decided not to include those results here. The second query just
picks the results from the map for a few airports. Those results are shown.

[source,groovy]
----
// How many flights are there from each airport?
g.V().hasLabel('airport').out().groupCount().by('code')

// count the routes from all the airports and then select a few.
g.V().hasLabel('airport').out().groupCount().by('code').
      select('AUS','AMS','JFK','DUB','MEX')

[AUS:98,AMS:285,JFK:203,DUB:185,MEX:116]
----

This next query essentially asks the same question about how many outgoing routes
each airport has. However, rather than return the count for each airport
individually, it groups the ones with the same number of routes together. As this
query returns a lot of data we just included a few lines from the full result below
the query.

[source,groovy]
----
// Same query except sorted into groups by ascending count
g.V().hasLabel('airport').
      group().by(out().count()).by('code').
      order(local).by(keys)
----

As can be seen this time the count value is the key and the airport codes are the
values.

[source,groovy]
----
[..., 73:[ALG],74:[MEL,KWI,SVQ,SVX],75:[BNA,CLE,LPL,YYC,SOF,EIN],76:[AMM,CTA],77:[CGK],78:[STL,RHO,HAJ,TLS],79:[NTE],80:[LCA,PDX,PSA,KIX,OVB],81:[LGA,SAN,HER],82:[BNE,BSL],83:[FAO,RAK],84:[HND,BOG,XMN,WUH],85:[MDW,KEF],86:[DMK],87:[MNL],88:[PTY,SKG],89:[IBZ,JNB],90:[TPA,VLC],91:[BWI,CAI,HGH,CSX],92:[GLA,BOD],94:[BOM,ADD,BLQ,LPA,RUH],95:[OPO,SFB],96:[DCA,RIX],97:[GRU],98:[AUS],99:[VCE],100:[AYT,BRS,KBP],102:[SYD,OTP,SHJ],103:[AUH,CMN],104:[SLC,KRK],106:[YVR,TFS],110:[NAP],111:[CUN],112:[JED],113:[LYS],115:[NCE,SAW],116:[ALC,MEX,WAW,KMG],117:[MRS,CRL,XIY],118:[PHX,DEL],119:[MLA],121:[STR],122:[CKG],123:[YUL,VKO],125:[HAM],126:[SEA],127:[BUD,TPE],128:[HEL,GVA,OSL],129:[SZX,BGY],130:[LTN,BHX],136:[EDI],138:[CGN],139:[ORY,LIS,CTU],140:[AGP],141:[NRT,PRG],143:[BOS,TLV],145:[DTW,KUL],147:[PHL],151:[MXP],153:[BKK,PMI],155:[ATH],156:[MSP],157:[SFO],158:[FLL,IAD],159:[LED],161:[LAS],165:[MCO],168:[ARN],169:[SIN],173:[ICN],174:[HKG],175:[CAN],180:[ZRH],181:[SVO],184:[CLT],185:[DUB],187:[DOH],194:[BRU,CPH],195:[YYZ],196:[MIA],199:[IAH,DUS],201:[FCO],202:[BER],203:[EWR,BCN],204:[JFK],206:[MAD,VIE],211:[STN],213:[PVG],214:[LAX],216:[MAN],217:[DEN],221:[LHR],232:[LGW,DME],242:[ATL],248:[DXB,PEK],253:[DFW],265:[ORD],270:[MUC],283:[AMS],293:[CDG],309:[IST],310:[FRA]]
----

As the above query returns a lot of data, we can also extract specific values we are
interested in as follows. Only airports with 62 outgoing routes are selected.

[source,groovy]
----
// Which of these airports have 105 outgoing routes?
g.V().hasLabel('airport').
      group().by(out().count()).by('code').
      select(constant(62L))
----

NOTE: The 'select' step does not allow direct selection of non-string keys. To work
around this you use its 'Traversal' form to access a numeric key from a result.

This time the results only include the airports with 62 outgoing routes.

[source,groovy]
----
[OAK,BEG,CVG,CAG]
----

[[groupvar]]
==== Using groupCount with a traversal variable

So far we have just used 'groupCount' with no parameters. When used in that way,
'groupCount' behaves like a 'map' step in that it passes the transformed data on to
the next step. However, if you specify the name of a traversal variable as a
parameter, the results of the count will be stored in that variable and 'groupCount'
will act the same way as a 'sideEffect' would, nothing is passed on from 'groupCount'
to the next step. We can use this capability to keep track of things during a query
while not actually changing the overall state of the traversal.

The example below starts at the vertex 'V(3)' and goes 'out' from there. A
'groupCount' step is then used to group the vertices we visited by a count of the
number of runways each has. We then go 'out' again and count how many vertices we
found and save that result in the variable 'b'. Note that the 'groupCount' when used
in this way did not pass anything on to the following step. Finally we use 'select'
to return our two variables as the results of the query.

[source,groovy]
----
g.V(3).out().groupCount('a').by('runways').
       out().count().as('b').select('a','b')

[a:[1:9,2:24,3:30,4:24,5:5,6:4,7:2],b:8354]
----

In the next section we will see another example of a 'groupCount' step that uses a
traversal variable.

[[gcconstant]]
==== Combining 'groupCount' and 'constant'

You can use a 'constant' value in combination with a 'groupCount' step as one way of
setting the name of the key that will be used in the result. The example below shows
a 'groupCount' step that is provided a traversal variable '"a"' as a parameter and a
constant as part of the 'by' modulator. The query starts by finding any airports in
the US state of Oklahoma. A 'constant' step is used to tell the 'groupCount' step
what to use as the key name in the result. At the end of the query a 'cap' step is
used to close and return the contents of '"a"'.

[source,groovy]
----
g.V().has('airport','region','US-OK').groupCount('a').by(constant('OK')).cap('a')
----

If we run the query, here is what we will get back. Notice the key is our constant
value '"OK"' and the value is the number of airports found in Oklahoma.

[source,groovy]
----
[OK:4]
----

The example above is intended purely to demonstrate the fact that you can use a
'constant' value with 'groupCount'. However, for this specific example, you would
probably code a query something like the one below instead.

[source,groovy]
----
g.V().has('airport','region','US-OK').groupCount().by('region')

[US-OK:4]
----

[[chainchoose]]
==== Combining 'choose' and 'groupCount'

Using the ideas discussed above, we can write a query that chains several
'choose' steps together to build a result set made up of various key:value pairs
representing different things that we are interested in counting. The query below
looks at all vertices that represent an airport. For each vertex found, various
properties are examined using a 'choose' step. If the test returns 'true' a count,
stored in the traversal variable "'a"' is incremented. Note how each of the choose
steps is 'dot chained' together. When used in this way 'choose' behaves in the same
way as a 'sideEffect' in that all of the airport vertices are passed to the next
choose rather than just those that pass the _if_ test.

[source,groovy]
----
g.V().hasLabel('airport').
      choose(has('runways',4), groupCount('a').by(constant('four'))).
      choose(has('runways',lte(2)), groupCount('a').by(constant('low'))).
      choose(has('runways',gte(6)), groupCount('a').by(constant('high'))).
      choose(has('country','FR'), groupCount('a').by(constant('France'))).
      groupCount('a').by(constant('total')).cap('a')
----

If we run the query this is what we might get back. We counted 3504 total airports,
found six airports that have six or more runways, 53 that had exactly four runways,
3204 that have two or fewer and found 59 airports in France.

[source,groovy]
----
[high:6,total:3504,low:3204,four:53,France:59]
----

Using 'choose' and 'groupCount' in this way provides a very nice pattern for counting
somewhat disparate things during a traversal.

[[nestedgroup]]
==== Nesting one 'group' step inside another

Many examples of both the 'groupCount' and 'group' steps have already been presented
in this book. However, something we have not touched on so far, which by now may be
obvious but perhaps not, is that you can nest one 'group' step inside another one.
This is made possible by the fact that the 'by' modulators that are used to tell the
'group' step precisely what you want grouped can take arbitrary traversals. Take a
look at the example below. Hopefully the indentation makes it easier to read. We
begin by simply finding five airports and starting a group. The key for each group
will be one of the codes for each of these airports. This is specified by the first
'by' modulator. For the value part of each group, specified by the second 'by'
modulator, we start a second traversal. That traversal begins by finding five places
you can fly to from each of the five airports that we initially found. Then another
'group' step is used to group each of those airports by the number of total outgoing
routes they have.

[source,groovy]
----
g.V().hasLabel("airport").limit(5).
      group().
        by('code').
        by(out("route").limit(5).
           group().
             by('code').
             by(out("route").count()))
----

When the query is run here is what we get back. As expected the outer group has keys
that represent the first five airports that were found. The inner group has the five
airport destinations as the keys and their total outgoing route counts as their
values. We pretty printed the output a bit to make it easier to read.

[source,groovy]
----
[BNA:[DCA:96,BWI:91,BOS:143,ATL:242,AUS:98],
 ANC:[ORD:265,IAH:199,LAX:214,DFW:253,MSP:156],
 BOS:[DCA:96,BNA:75,BWI:91,ATL:242,AUS:98],
 ATL:[NRT:141,FRA:310,LHR:221,CDG:293,YYZ:195],
 AUS:[ONT:23,PHL:147,PDX:80,DTW:145,OKC:33]]
----

So what we have created is a group, essentially a map, that has airport codes as the
key and an additional group as the values. Therefore, given each group is made up of
key value pairs, we can use a 'select' step to only pick a few of the results.

[source,groovy]
----
g.V().hasLabel("airport").limit(5).
      group().
        by('code').
        by(out("route").limit(5).
           group().
             by('code').
             by(out("route").count())).
      select('ANC','ATL')
----

This time only the selected results are returned

[source,groovy]
----
[ANC:[ORD:265,IAH:199,LAX:214,DFW:253,MSP:156],
 ATL:[NRT:141,FRA:310,LHR:221,CDG:293,YYZ:195]]
----

If you want to select results from one of the inner groups you can do that as well
using an additional 'select' step.

[source,groovy]
----
g.V().hasLabel("airport").limit(5).
      group().
        by('code').
        by(out("route").limit(5).
           group().
             by('code').
             by(out("route").count())).
      select('ANC').
      select('IAH','LAX')
----

This will first select the result with a key of 'ANC' and then from that group select
the results for the keys of 'IAH' and 'LAX' only.

[source,groovy]
----
[IAH:199,LAX:214]
----

[[eu-usa]]
==== Analysis of routes between Europe and the USA

The next few queries show how you can use a graph like 'air-routes' to perform
analysis on a particular industry segment. The following queries analyze the
distribution and availability of routes between airports across Europe and airports
in the United States. First of all let's just find out how many total routes there 
are between airports anywhere in Europe and airports in the USA.

[source,groovy]
----
// How many routes from anywhere in Europe to the USA?
g.V().has('continent','code','EU').
      out().out().has('country','US').
      count()

435
----

So we now know that there are 435 different routes. Remember though that the
'air-routes' graph does not track the number of airlines that operate any of these
routes. The graph just stores the data that at least one airline operates each of
these unique route pairs. Let's dig a bit deeper into the 435 and find out how many
US airports have flights that arrive from Europe.

[source,groovy]
----
// How many different US airports have routes from Europe?
g.V().has('continent','code','EU').
      out().out().has('country','US').
      dedup().count()

45
----

So we can now see that the 435 routes from European airports arrive at one of 45
airports in the United States. We can dig a bit deeper and look at the distribution
of these routes across the 45 airports.

[source,groovy]
----
//What is the distribution of the routes amongst those US airports?
g.V().has('continent','code','EU').
      out().out().has('country','US').
      groupCount().by('code').
      order(local).by(values,asc)
----

John F. Kennedy airport (JFK) in New York appears to have the most routes from Europe
with Newark (EWR) having the second most.

[source,groovy]
----
[CHS:1,MCI:1,BNA:1,ANC:1,CLE:1,IND:1,STL:2,BDL:2,SJC:2,BWI:2,PHX:2,
 MSY:2,CVG:2,SAN:3,RDU:3,PDX:3,SLC:4,AUS:4,RSW:4,PIT:4,TPA:5,SFB:5,SWF:5,
 DTW:6,MSP:6,OAK:6,FLL:7,DEN:7,PVD:7,CLT:8,IAH:8,LAS:11,DFW:12,SEA:12,MCO:14,
 ATL:15,SFO:20,PHL:22,IAD:22,LAX:26,BOS:26,ORD:27,MIA:28,JFK:42,EWR:43]
----

Now let's repeat the process but looking at the European end of the routes. First of
all, we can calculate how many European airports have flights to the United States.

[source,groovy]
----
// How many European airports have service to the USA?
g.V().has('continent','code','EU').
      out().as('a').
      out().has('country','US').
      select('a').dedup().count()

62
----

Just as we did for the airports in the US we can figure out the distribution of
routes for the European airports.

[source,groovy]
----
// What is the distribution of US routes amongst
// the European airports?
g.V().has('continent','code','EU').
      out().as('a').
      out().has('country','US').
      select('a').groupCount().by('code').
      order(local).by(values,asc)
----

It appears that London Heathrow (LHR) offers the
most US destinations and Frankfurt (FRA) the second most.

[source,groovy]
----
[TER:1,RIX:1,BRS:1,DSA:1,DBV:1,TFS:1,KRK:1,BLQ:1,ORK:1,KBP:1,DME:1,BEG:1,AGP:1,
 PMI:1,HAM:1,RZE:1,NAP:1,PRG:2,OPO:2,STR:2,NCE:2,BHX:2,BFS:3,GVA:3,VCE:3,VKO:3,
 BUD:3,PDL:3,STN:4,BGO:4,SVO:4,ORY:4,GLA:4,WAW:5,SNN:5,ATH:5,MXP:6,CGN:6,HEL:6,
 VIE:6,BER:7,BRU:8,OSL:8,EDI:9,ARN:9,LIS:9,DUS:11,IST:11,CPH:11,BCN:12,MAD:13,
 FCO:13,MAN:14,LGW:14,ZRH:15,MUC:18,DUB:18,AMS:22,CDG:25,KEF:26,FRA:28,LHR:30]
----

Lastly, we can find out what the list of routes flown is. For this example we decided
to just return 10 of the 435 routes. Note how the 'path' step returns all parts of
the traversal including the continent code 'EU'. We could remove that part of the
result by adding a 'from' modulator as shown earlier in the "<<pathintro>>" section.

[source,groovy]
----
// Selected routes from Europe to the USA.
g.V().has('continent','code','EU').
      out().out().
      has('country','US').
      path().by('code').
      limit(10)

[EU,KRK,ORD]
[EU,TFS,EWR]
[EU,PDL,BOS]
[EU,PDL,JFK]
[EU,PDL,EWR]
[EU,DSA,SFB]
[EU,TER,BOS]
[EU,RZE,EWR]
[EU,LHR,PIT]
[EU,LHR,PDX]
----

[[mapreduce]]
==== Using 'fold' to do simple Map-Reduce computations

Earlier in the book we saw examples of 'sum' being used to count a
collection of values. You can also use 'fold' to do something similar but in a
more 'map-reduce' type of fashion.

First of all, here is a query that uses 'fold' in a way that we have already seen. It
will find all routes from Austin and uses a 'fold' step to return a list of those
names.

[source,groovy]
----
g.V().has('code','AUS').
      out('route').
      values('city').fold()
----

As expected the results show all of the cities that you can fly to from Austin
collected into a single list.

[source,groovy]
----
[Philadelphia,Portland,Detroit,Oaklahoma City,Ontario,Charlotte,Cancun,Memphis,
 Cincinnati,Indianapolis,Kansas City,Dallas,St Louis,Albuquerque,Milwaukee,Chicago,
 Omaha,Tulsa,Puerto Vallarta,Nassau,Little Rock,Jacksonville,Providence,Sacramento,
 Birmingham,Louisville,Buffalo,Boise,Lubbock,Panama City Beach,Harlingen,Reno,
 Columbus,Des Moines,Cozumel,Amarillo,Baton Rouge,Charleston,Guadalajara,
 Grand Rapids,Liberia,Pensacola,San JosÃ© del Cabo,Knoxville,Valparaiso,
 Fayetteville/Springdale/,Hayden,Orlando,Burbank,Aspen,Bozeman,Branson,St
 Petersburg-Clearwater,Atlanta,Nashville,Boston,Baltimore,Washington D.C.,Dallas,
 Fort Lauderdale,Washington D.C.,Houston,New York,Los Angeles,Orlando,Miami,
 Minneapolis,Chicago,Phoenix,Raleigh,Seattle,San Francisco,San Jose,Tampa,San Diego,
 Long Beach,Santa Ana,Salt Lake City,Las Vegas,Denver,San Antonio,Toronto,Honolulu,
 Vancouver,New Orleans,London,Newark,London,Houston,Frankfurt,El Paso,Amsterdam,
 Cleveland,Calgary,Oakland,Mexico City,Tucson,Pittsburgh]
----

However, what if we wanted to reduce our results further? Take a look at the modified
version of our query below. It finds all routes from Austin and looks at the names of
the destination cities. However, rather than return all the names, this time the
'fold' step is used differently and effectively reduces the city names to a single
value. That value being the total number of characters in all of those city names. We
have seen 'fold' used elsewhere in the book but this time we provide 'fold' with a
parameter and a closure. The parameter is passed to the closure as the first variable
and the name of the city as the second. The closure then adds the zero and the length
of each name effectively producing a running total.

[source,groovy]
----
g.V().has('code','AUS').out('route').values('city').
      fold(0) {a,b -> a + b.length()}

891
----

NOTE: While this query will work as-is on TinkerGraph within the Gremlin Console,
some graph systems are more strict about their type checking and sandboxing of Groovy
closures. To be on the safe side you can always explicitly type cast the closure as
follows.

[source,groovy]
----
g.V().has('code','AUS').out('route').values('city').
                       fold(0) {a,b -> (int)a + ((String)b).length()}
----

The point of the previous example with 'fold' using a closure was to demonstrate a
subtle feature of it in the context of reducing results. The more direct way to get
the same result is with standard Gremlin steps:

[source,groovy]
----
g.V().has('code','AUS').out('route').values('city').
      length().sum()
----

[[meanmode]]
==== Distribution of routes in the graph (mode and mean)

An example of a common question we might want to answer with a network graph, of
which air routes are an example, is "how are the routes in our graph distributed
between airport vertices?". We can also use this same query to find the statistical
'mode' (most common number) for a set of routes.

Take a look at the next query that shows how we can do analysis on the distribution
of routes throughout the graph. We are only interested in vertices that are airports
and for those vertices we want to count how many outgoing routes each airport has. We
want to return the results as a set of ordered 'key:value' pairs where the key is the
number of outgoing routes and the value is the number of airports that have that
number of outgoing routes.

[source,groovy]
----
g.V().hasLabel('airport').
      groupCount().by(out('route').count()).
      order(local).by(values,desc)
----

When we run the query we get back the results below. As the results are sorted in
descending order by value, we can see that the 'mode' (most common) number of
outgoing routes is actually just one route and that 786 airports have just one
outgoing route. We can see that 654 airports have just two routes and so on. We can
also see at the other end of the scale that one airport has 237 outgoing routes.

[source,groovy]
----
[1:777,2:649,3:357,4:234,5:149,6:140,7:100,8:73,9:64,10:59,11:55,12:49,13:43,16:36,
15:31,0:29,20:27,18:24,14:21,17:19,19:18,21:18,22:18,24:15,25:15,23:14,27:14,41:13,
34:12,36:12,26:11,35:11,33:10,38:10,45:10,28:9,32:9,57:9,67:9,42:8,44:8,46:8,49:8,
29:7,37:7,51:7,30:6,52:6,53:6,56:6,75:6,39:5,40:5,43:5,48:5,58:5,61:5,66:5,71:5,80:5,
94:5,31:4,47:4,54:4,55:4,60:4,62:4,63:4,68:4,74:4,78:4,84:4,91:4,116:4,59:3,69:3,
81:3,100:3,102:3,117:3,128:3,139:3,64:2,65:2,76:2,82:2,83:2,85:2,88:2,89:2,90:2,
92:2,95:2,96:2,103:2,104:2,106:2,115:2,118:2,123:2,127:2,129:2,130:2,141:2,143:2,
145:2,153:2,158:2,194:2,199:2,203:2,206:2,232:2,248:2,265:1,270:1,283:1,293:1,309:1,
310:1,72:1,73:1,77:1,79:1,86:1,87:1,97:1,98:1,99:1,110:1,111:1,112:1,113:1,119:1,
121:1,122:1,125:1,126:1,136:1,138:1,140:1,147:1,151:1,155:1,156:1,157:1,159:1,161:1,
165:1,168:1,169:1,173:1,174:1,175:1,180:1,181:1,184:1,185:1,187:1,195:1,196:1,201:1,
202:1,204:1,211:1,213:1,214:1,216:1,217:1,221:1,242:1,253:1]
----

We could change our query above, replacing 'out()' with '__.in()' and we could find
out the distribution of incoming routes. Remembering that in an air route network
there is not always a one to one equivalent number of outgoing to incoming routes due
to the way airlines plan their routes.

Another change we could make to our query is to change the ordering to use the key
field for each key:value pair and this time sort in ascending order.

[source,groovy]
----
g.V().hasLabel('airport').
      groupCount().by(out('route').count()).
      order(local).by(keys,asc)
----

When we run our query again we get the results below. Looking at the data sorted
this way helps some new interesting facts stand out. The most interesting thing
we can immediately spot is that there are 16 airports that currently have no
outgoing routes at all!

[source,groovy]
----
[0:29,1:777,2:649,3:357,4:234,5:149,6:140,7:100,8:73,9:64,10:59,11:55,12:49,13:43,
14:21,15:31,16:36,17:19,18:24,19:18,20:27,21:18,22:18,23:14,24:15,25:15,26:11,27:14,
28:9,29:7,30:6,31:4,32:9,33:10,34:12,35:11,36:12,37:7,38:10,39:5,40:5,41:13,42:8,
43:5,44:8,45:10,46:8,47:4,48:5,49:8,51:7,52:6,53:6,54:4,55:4,56:6,57:9,58:5,59:3,
60:4,61:5,62:4,63:4,64:2,65:2,66:5,67:9,68:4,69:3,71:5,72:1,73:1,74:4,75:6,76:2,
77:1,78:4,79:1,80:5,81:3,82:2,83:2,84:4,85:2,86:1,87:1,88:2,89:2,90:2,91:4,92:2,
94:5,95:2,96:2,97:1,98:1,99:1,100:3,102:3,103:2,104:2,106:2,110:1,111:1,112:1,113:1,
115:2,116:4,117:3,118:2,119:1,121:1,122:1,123:2,125:1,126:1,127:2,128:3,129:2,130:2,
136:1,138:1,139:3,140:1,141:2,143:2,145:2,147:1,151:1,153:2,155:1,156:1,157:1,158:2,
159:1,161:1,165:1,168:1,169:1,173:1,174:1,175:1,180:1,181:1,184:1,185:1,187:1,194:2,
195:1,196:1,199:2,201:1,202:1,203:2,204:1,206:2,211:1,213:1,214:1,216:1,217:1,221:1,
232:2,242:1,248:2,253:1,265:1,270:1,283:1,293:1,309:1,310:1]
----

If we wanted to find the statistical mean number of routes in the graph we could
easily write a query like the one below to tell us how many airports and
outgoing routes in total there are in the graph.

[source,groovy]
----
g.V().hasLabel('airport').union(count(),out('route').count()).fold()

[3504,50637]
----

We could then use the Gremlin Console do the division for us to calculate the mean.

[source,groovy]
----
gremlin> 50637/3504

14.4511986301
----

However, Gremlin also has a 'mean' step that we can take advantage of if we can
figure out a way to use it in this case that will do the work for us. Take a look at
the next query. The key thing to note here is the way 'local' has been used. This
will cause Gremlin to essentially do what we did a bit more manually above. If we did
not include 'local' the answer would just be the total number of outgoing routes as
Gremlin would essentially calculate 50637/1. By using local we force Gremlin to in
essence create an array containing the number of routes for each airport, add those
values up and divide by the number of elements in the array (the number of airports).
We hope that makes sense. If it is confusing try the query yourself on the gremlin
console with and without local and try it without the 'mean' step. You will see all
of the interim values instead!

[source,groovy]
----
g.V().hasLabel('airport').local(out('route').count()).mean()

14.4511986301
----

So it seems there is an average of just over 12 outgoing routes per airport in
the graph whichever way we decide to calculate it!

Now that we have a query figured out for calculating the average number of
outgoing routes per airport, we can easily tweak it to do the same for incoming
routes and combined, incoming and outgoing, routes.

[source,groovy]
----
// Average number of incoming routes
g.V().has('type','airport').local(__.in('route').count()).mean()

14.4511986301

// Average number of outgoing and incoming routes
g.V().has('type','airport').local(both('route').count()).mean()

28.902397260273972
----

==== How many routes are there from airports in London (UK)?

This next query can be used to figure out how many outgoing routes each of the
airports classified as being in (or near) London, England has. Note that we
first find all airports in England using 'has('region','GB-ENG')'. If we did
not do this we would pick up airports in other countries as well such as
London, Ontario, in Canada.

[source,groovy]
----
g.V().has('region','GB-ENG').has('city','London').
      group().by('code').by(out().count())

[LCY:51,LHR:221,LTN:130,STN:211,LGW:232]
----

Here is a twist on the above theme. How many places can we get to from London in two
hops but not including flights that end up back in London? It turns out there are
over 2,000 places!  Notice how 'aggregate' is used to store the set of London
airports as a collection that can be referenced later on in the query to help with
ruling out any flights that would end up back in London.

[source,groovy]
----
// Leave from London, fly with one stop, not ending back in London, how many places?
g.V().has('region','GB-ENG').has('city','London').aggregate('lon').
      out().out().dedup().where(without('lon')).count()

2353
----

We could have written the previous query like this and avoided using 'aggregate' but
to me, this feels more clumsy and somewhat repetitive.

[source,groovy]
----
g.V().has('region','GB-ENG').has('city','London').out().out().dedup().
      not(and(has('city','London'),has('region','GB-ENG'))).count()

2353
----

[[englandroutes]]
==== How many routes are there between airports in England?

We can use an 'aggregate' step to quite elegantly find the routes that exist between
airports located in England. The following query finds all airports in England and
orders them by airport code. It then collects those airports using an 'aggregate'
step. Finally the aggregated collection is used to find routes to airports also
within the UK. Note that as written, this query finds routes in both directions if
they exist.

[source,groovy]
----
// Flights within England
g.V().has('region','GB-ENG').order().by('code').aggregate('a').
      out().where(within('a')).path().by('code')
----

Here are the routes found when the query is run. We arranged the results into columns
to save space.

[source,groovy]
----
[BHX,NCL]  [BRS,NCL]  [EMA,SOU]  [EXT,MAN]
[EXT,NQY]  [EXT,ISC]  [HUY,NWI]  [ISC,NQY]
[ISC,EXT]  [ISC,LEQ]  [LBA,LHR]  [LBA,SOU]
[LBA,NQY]  [LCY,MAN]  [LCY,NCL]  [LEQ,ISC]
[LGW,NCL]  [LGW,NQY]  [LHR,LBA]  [LHR,NCL]
[LHR,NQY]  [LHR,MAN]  [LPL,NQY]  [MAN,LHR]
[MAN,LCY]  [MAN,SOU]  [MAN,NQY]  [MAN,SEN]
[MAN,NWI]  [MAN,EXT]  [MME,NWI]  [NCL,SOU]
[NCL,BRS]  [NCL,BHX]  [NCL,LHR]  [NCL,LGW]
[NCL,LCY]  [NQY,SEN]  [NQY,EXT]  [NQY,ISC]
[NQY,LHR]  [NQY,LGW]  [NQY,MAN]  [NQY,LPL]
[NQY,LBA]  [NWI,MAN]  [NWI,HUY]  [NWI,MME]
[SEN,NQY]  [SEN,MAN]  [SOU,NCL]  [SOU,MAN]
[SOU,EMA]  [SOU,LBA]
----

Here is a different way the query can be written. This time a 'where' step is used to
filter out the previously seen airport pairs so we only get routes in one direction.

[source,groovy]
----
g.V().has('region','GB-ENG').order().by('code').as('a').
      out().has('region','GB-ENG').as('b').
      where('a',lt('b')).by('code').
      path().by('code')
----

As you can see this time we only get each airport pair back once regardless of route
direction.

[source,groovy]
----
[BHX,NCL]
[BRS,NCL]
[EMA,SOU]
[EXT,MAN]
[EXT,NQY]
[EXT,ISC]
[HUY,NWI]
[ISC,NQY]
[ISC,LEQ]
[LBA,LHR]
[LBA,SOU]
[LBA,NQY]
[LCY,MAN]
[LCY,NCL]
[LGW,NCL]
[LGW,NQY]
[LHR,NCL]
[LHR,NQY]
[LHR,MAN]
[LPL,NQY]
[MAN,SOU]
[MAN,NQY]
[MAN,SEN]
[MAN,NWI]
[MME,NWI]
[NCL,SOU]
[NQY,SEN]
----

[[topten]]
==== What are the top ten airports by route count?

Earlier we calculated which airports have the most routes. These next three queries
are of a similar nature but produce a tables of the top ten airports in terms of
incoming, outgoing and overall routes. As mentioned before, because of the way some 
airlines route flights, the number of outgoing and incoming routes to an airport will
not always be the same. For example, several KLM Airlines flights from Amsterdam to
airports in Africa continue on to other African airports before returning to
Amsterdam. As a result there are more inbound routes from than out bound routes to
these airports. In each example below the 'project' step is used to generate the
results in a easily readable form.

First of all, this query will find the ten airports with the most incoming routes,
sorted in descending order.

[source,groovy]
----
// Find the top ten overall in terms of incoming routes
g.V().hasLabel('airport').
      order().by(__.in('route').count(),desc).limit(10).
      project('ap','routes').by('code').by(__.in('route').count())
----

So it seems that Frankfurt and Amsterdam are tied for the most incoming routes. It is
worth remembering that the version of the graph used for the examples in the book
represents a moment in time. If these queries are re run at a later date on a newer
version of the graph the results will quite likely be different. This is because
airlines regularly add, and in some cases drop, routes.

[source,groovy]
----
[ap:FRA,routes:310]
[ap:IST,routes:309]
[ap:CDG,routes:294]
[ap:AMS,routes:285]
[ap:MUC,routes:271]
[ap:ORD,routes:264]
[ap:DFW,routes:253]
[ap:PEK,routes:249]
[ap:DXB,routes:248]
[ap:ATL,routes:242]
----

Now let's do the same thing but for outgoing routes.

[source,groovy]
----
// Find the top ten overall in terms of outgoing routes
g.V().hasLabel('airport').
      order().by(out('route').count(),desc).limit(10).
      project('ap','routes').by('code').by(out('route').count())
----

This time Frankfurt has the most routes. This reinforces a point that we made
earlier. The number of incoming and outgoing routes for a given airport will not
always be the same. This is because of the way airlines route flights. Some flights
continue to other destinations before returning so there will not always be direct
flights between airport pairs in both directions. We know as a passenger this is
something that we find frustrating!

[source,groovy]
----
[ap:FRA,routes:310]
[ap:IST,routes:309]
[ap:CDG,routes:293]
[ap:AMS,routes:283]
[ap:MUC,routes:270]
[ap:ORD,routes:265]
[ap:DFW,routes:253]
[ap:DXB,routes:248]
[ap:PEK,routes:248]
[ap:ATL,routes:242]
----

Lastly, let's find the top ten airports ordered by the total number of incoming and
outgoing routes that they have.

[source,groovy]
----
// Find the top ten overall in terms of total routes
g.V().hasLabel('airport').
      order().by(both('route').count(),desc).limit(10).
      project('ap','routes').by('code').by(both('route').count())
----

As expected, Frankfurt is the airport with the most overall routes. One small
side note while on the topic of airline routes. The graph does not track the 
frequency at which any given route is operated. What this means is that the airport 
with the most routes is not necessarily also the busiest. This is because many routes
are operated multiple times a day. We did not try to include this data in the graph 
as it changes too often for me to keep up with.

[source,groovy]
----
[ap:FRA,routes:620]
[ap:IST,routes:618]
[ap:CDG,routes:587]
[ap:AMS,routes:568]
[ap:MUC,routes:541]
[ap:ORD,routes:529]
[ap:DFW,routes:506]
[ap:PEK,routes:497]
[ap:DXB,routes:496]
[ap:ATL,routes:484]
----

[[localfold]]
==== Using 'local' while counting things

In some of the earlier sections we saw examples of 'local' scope being used. Here is
another example of how 'local' scope can be used while counting things to achieve a
desired result.

Take a look at the query below. It finds any airports that have six or more runways
and then returns the airport's IATA code along with the number of runways it has.

[source,groovy]
----
g.V().has('airport','runways',gte(6)).values('code','runways').fold()
----

Here is the output from running the query. As you can see what is returned is a list
of airport codes with each followed by its runway count.

[source,groovy]
----
[BOS,6,DFW,7,ORD,7,DEN,6,DTW,6,AMS,6]
----

While the output returned by the previous query is not bad, it might be nice to have
what is returned be a set of code and runway pairs each in its own list. We can
achieve this result by having the 'fold' step applied to the interim or 'local'
results of the query.

Take a look at the modified form of the query below. Part of the query is now wrapped
inside of a 'local' step.

[source,groovy]
----
g.V().has('airport','runways',gte(6)).local(values('code','runways').fold())
----

Here is the output from running our modified form of the query. Each airport code and
runway value pair is now in its own individual list.

[source,groovy]
----
[BOS,6]
[DFW,7]
[ORD,7]
[DEN,6]
[DTW,6]
[AMS,6]
----

[[noedges]]
==== How many vertices have no edges?

In the air routes graph there are some vertices that have no outgoing edges, some
that have no incoming edges and a few, such as the vertex for the continent
of Antarctica, that have neither. We can use some simple
queries to count how many of each type exist.

[source,groovy]
----
// Vertices with no outgoing edges.
g.V().not(outE()).count()

36

// Vertices with no incoming edges.
g.V().not(inE()).count()

245

// Vertices with no edges.
g.V().not(bothE()).count()

7
----

The queries above provide a nice shorthand way of writing queries that we could also
write using 'where' steps. As shown below.

[source,groovy]
----
g.V().where(outE().count().is(0)).count()

36
----

[[wherefly]]
=== Where can I fly to from here?

In this section you will find some more examples of queries that explore different
questions along the lines of "Where can I fly to from here?".

[[doesanyrouteexist]]
==== Does any route exist between two airports?

We have already looked at different ways to discover shortest routes (by hops)
between two airports using queries such as the one below. This query looks for a
single route between Austin (AUS) and the Canadian city of Peawanuck in Ontario. It
just so happens that Peawanuck is one of the hardest places to get to from Austin in
the whole air-routes graph. In fact, to get there, requires six stops along the way!
With that in mind, the query below can easily run out of memory or time out trying to
find even a single route as there is so much work to do to analyze all of the
possible routes.


[source,groovy]
----
g.V().has('code','AUS').
      repeat(out().simplePath()).
        until(has('code','YPO')).
      limit(1).
      path().by('code')
----

In cases such as this we can take a slightly different approach that will let us know
if any route exists and in fact will execute very efficiently. Note that this query
cannot find multiple routes but is very useful when trying to answer the question
"does any route exist?".

[source,groovy]
----
g.V().has('code','AUS').
      repeat(out().dedup()).
        until(has('code','YPO')).
      path().by('code')

[AUS,YYZ,YTS,YMO,YFA,ZKE,YAT,YPO]
----

The difference between the two queries is the addition of a 'dedup' step. This
ensures we only ever visit any airport along the route once no matter how we got
there. This is different from the 'simplePath' step which makes sure we do not loop
back on ourselves during a traversal but allows us to visit the same airport multiple
times so long as we got there using a different path. Even if we were to make the
task of finding a route harder by explicitly avoiding a specific stop the execution
remains very efficient due to the 'dedup' step being used.

[source,groovy]
----
g.V().has('code','AUS').
      repeat(out().has('code',without('YYZ')).dedup()).
        until(has('code','YPO')).
      path().by('code')

[AUS,MDW,YTZ,YTS,YMO,YFA,ZKE,YAT,YPO]
----

Keep in mind that this trick is only useful when trying to figure out if any route
(or path) exists in a group between a pair of vertices. It cannot be used to find
multiple routes but is still a very useful pattern to be aware of. You will see a
variation of this technique used again in the "<<longestroutes>>" section.

[[uscatoindia]]
==== Where in the USA or Canada can I fly to from any airport in India?

This first query looks for routes to the USA or Canada from any airport in India.

[source,groovy]
----
// Where in the USA or Canada can I fly to from any airport in India?
g.V().has('country','code','IN').out().out().
      has('country',within('US','CA')).path().by('code')
----

Here are the results of running the query. Note that because we used the 'path' step
the country code for India "IN" is included in the output.

[source,groovy]
----
[IN,DEL,IAD]
[IN,DEL,JFK]
[IN,DEL,ORD]
[IN,DEL,SFO]
[IN,DEL,EWR]
[IN,DEL,YYZ]
[IN,DEL,YVR]
[IN,BOM,JFK]
[IN,BOM,EWR]
[IN,BOM,YYZ]
----

[[flleurope]]
==== Which cities in Europe can I fly to from Ft. Lauderdale in Florida?

This query looks for routes from Fort Lauderdale in Florida to cities in Europe.

[source,groovy]
----
// Where can I fly to in Europe from Ft. Lauderdale?
g.V().has('code','FLL').out().as('a').in('contains').
      has('code','EU').select('a').values('city')
----

Here are the results of running the query.

[source,groovy]
----
London
Paris
Barcelona
Madrid
Oslo
Stockholm
Copenhagen
----

[[clteusa]]
==== Where can I fly to from Charlotte, to cities in Europe or South America?

This query looks for routes from Charlotte, North Carolina to cities in Europe or
South America.

[source,groovy]
----
// Flights from Charlotte to airports in Europe or South America
g.V().has('code','CLT').out().as('a').in('contains').
      has('code',within('EU','SA')).select('a').by('code')
----

Here are the results of running the query.

[source,groovy]
----
LHR
CDG
FRA
DUB
FCO
BCN
MAD
MUC
GIG
GRU
----

[[usalon]]
==== Where in the United States can I fly from to airports in London?

This query looks for routes from any one of the five airports in the
London area in the UK that end up in the United States.

[source,groovy]
----
// Where in the United States can I fly to non-stop from any of the
// airports in and around London in the UK?
g.V().has('airport','code',within('LHR','LCY','LGW','LTN','STN')).
      out().has('country','US').path().by('code')
----

Here are the results of running the query.

[source,groovy]
----
[LHR,PIT]  [LHR,PDX]  [LHR,CLT]
[LHR,CHS]  [LHR,ATL]  [LHR,AUS]
[LHR,BNA]  [LHR,BOS]  [LHR,BWI]
[LHR,DFW]  [LHR,IAD]  [LHR,IAH]
[LHR,JFK]  [LHR,LAX]  [LHR,MIA]
[LHR,MSP]  [LHR,ORD]  [LHR,PHX]
[LHR,RDU]  [LHR,SEA]  [LHR,SFO]
[LHR,SJC]  [LHR,SAN]  [LHR,SLC]
[LHR,LAS]  [LHR,DEN]  [LHR,MSY]
[LHR,EWR]  [LHR,PHL]  [LHR,DTW]
[LGW,AUS]  [LGW,BOS]  [LGW,FLL]
[LGW,JFK]  [LGW,LAX]  [LGW,MCO]
[LGW,MIA]  [LGW,ORD]  [LGW,SEA]
[LGW,SFO]  [LGW,TPA]  [LGW,LAS]
[LGW,DEN]  [LGW,OAK]  [STN,BOS]
[STN,IAD]  [STN,EWR]  [STN,SFB]
----

[[txny]]
==== Where in New York state can I fly to from any of the airports in Texas?

This query looks for all the routes from any airport in Texas that end up in any of
the New York state airports.

[source,groovy]
----
// Where in New York state can I fly to from any airport in Texas?
g.V().has('airport','region','US-TX').out().has('region','US-NY').path().by('code')
----

Here are the results of running the query.

[source,groovy]
----
[AUS,BUF]
[AUS,JFK]
[AUS,EWR]
[DFW,BUF]
[DFW,JFK]
[DFW,LGA]
[DFW,EWR]
[IAH,JFK]
[IAH,LGA]
[IAH,EWR]
[SAT,JFK]
[SAT,EWR]
[HOU,JFK]
[HOU,LGA]
[HOU,EWR]
[DAL,LGA]
----

[[denmex]]
==== Which cities in Mexico can I fly to from Denver?

This query looks for routes from Denver to anywhere in Mexico.

[source,groovy]
----
// Where in Mexico can I fly to from Denver?
g.V().has('code','DEN').out().has('country','MX').values('city')
----

Here are the results of running the query.

[source,groovy]
----
Mexico City
Cancun
Puerto Vallarta
Cozumel
Monterrey
Guadalajara
San JosÃ© del Cabo
----

[[deleu]]
==== Which cities in Europe can I fly to from Delhi in India?

This query looks for routes from Delhi that go to any airport in Europe.

[source,groovy]
----
// Where in Europe can I fly to from Delhi?
g.V().has('code','DEL').out().as('a').in("contains").
      has('code','EU').select('a').by('city')
----

Here are the results of running the query.

[source,groovy]
----
London
Paris
Frankfurt
Helsinki
Rome
Amsterdam
Madrid
Vienna
Zurich
Brussels
Munich
Stockholm
Moscow
Milan
Istanbul
Copenhagen
Reykjavik
Kiev
Birmingham
----

[[aggrroutes]]
==== Finding all routes between London, Munich and Paris

In the following example we find all the routes between airports in London, Munich
and Paris. Notice how by using 'aggregate' to collect the results of the first
'within' test that we don't have to repeat the names in the second 'within', we can
just refer to the aggregated collection.

[source,groovy]
----
g.V().has('city',within('London','Munich','Paris')).aggregate('a').out().
      where(within('a')).path().by('code')
----

Here is what we might get back from the query. We have laid the results out in
columns to save space.

[source,groovy]
----
[LHR,ORY] [LHR,CDG] [LHR,MUC] [LGW,CDG]
[LGW,MUC] [CDG,LTN] [CDG,LHR] [CDG,LGW]
[CDG,MUC] [CDG,LCY] [MUC,LTN] [MUC,LHR]
[MUC,LGW] [MUC,CDG] [MUC,LCY] [MUC,STN]
[MUC,ORY] [LCY,CDG] [LCY,MUC] [LCY,ORY]
[STN,MUC] [ORY,LHR] [ORY,MUC] [ORY,LCY]
[LTN,CDG] [LTN,MUC] [LTN,ORY]
----

[[moredist]]
=== More analysis of distances between airports

In this section you will find some more queries that examine distances between
airports. The query below returns a nice list of all the routes from Austin (AUS)
along with their distances. The results are sorted in ascending order by distance.

[source,groovy]
----
// Distances of all routes from AUS along with destination IATA CODE
g.V().has('code','AUS').outE().order().by('dist',asc).
      inV().path().by('code').by('dist')
----

Here are the results of running the query. For ease of reading we again broke the
results into four columns.

[source,groovy]
----
[AUS,66,SAT]   [AUS,142,IAH]  [AUS,152,HOU]  [AUS,189,DAL]  [AUS,190,DFW]
[AUS,274,HRL]  [AUS,341,LBB]  [AUS,359,OKC]  [AUS,389,BTR]  [AUS,419,AMA]
[AUS,427,TUL]  [AUS,444,MSY]  [AUS,446,LIT]  [AUS,463,XNA]  [AUS,508,BKG]
[AUS,527,ELP]  [AUS,558,MEM]  [AUS,618,ABQ]  [AUS,625,PNS]  [AUS,650,MCI]
[AUS,664,VPS]  [AUS,681,BHM]  [AUS,708,ECP]  [AUS,722,STL]  [AUS,748,MEX]
[AUS,755,GDL]  [AUS,755,BNA]  [AUS,768,DEN]  [AUS,773,OMA]  [AUS,795,TUS]
[AUS,809,PVR]  [AUS,809,ATL]  [AUS,812,ASE]  [AUS,814,DSM]  [AUS,866,PHX]
[AUS,875,SDF]  [AUS,881,TYS]  [AUS,887,SJD]  [AUS,890,HDN]  [AUS,917,PIE]
[AUS,919,IND]  [AUS,922,CUN]  [AUS,925,TPA]  [AUS,945,CZM]  [AUS,952,JAX]
[AUS,957,CVG]  [AUS,972,MDW]  [AUS,973,ORD]  [AUS,992,SFB]  [AUS,994,MCO]
[AUS,1030,CLT] [AUS,1032,MKE] [AUS,1040,MSP] [AUS,1053,CHS] [AUS,1072,CMH]
[AUS,1080,SLC] [AUS,1080,LAS] [AUS,1101,MIA] [AUS,1103,GRR] [AUS,1110,FLL]
[AUS,1140,DTW] [AUS,1159,RDU] [AUS,1160,SAN] [AUS,1173,CLE] [AUS,1193,ONT]
[AUS,1206,SNA] [AUS,1209,PIT] [AUS,1220,LGB] [AUS,1230,LAX] [AUS,1238,BUR]
[AUS,1284,NAS] [AUS,1294,IAD] [AUS,1298,BZN] [AUS,1313,DCA] [AUS,1339,BWI]
[AUS,1357,YYZ] [AUS,1364,BUF] [AUS,1373,BOI] [AUS,1402,RNO] [AUS,1430,PHL]
[AUS,1476,SJC] [AUS,1477,SMF] [AUS,1493,OAK] [AUS,1500,SFO] [AUS,1500,EWR]
[AUS,1520,JFK] [AUS,1562,LIR] [AUS,1660,PVD] [AUS,1671,YYC] [AUS,1690,BOS]
[AUS,1712,PDX] [AUS,1768,SEA] [AUS,1869,YVR] [AUS,3755,HNL] [AUS,4901,LHR]
[AUS,4921,LGW] [AUS,5074,AMS] [AUS,5294,FRA]
----

This query finds all routes from DFW that are longer than 4,000 miles and returns the
airport codes and the distances. Notice the use of two 'by' modulators in this query
to decide which values are returned from the source vertex, the edge and the
destination vertex respectively. Also note that only two were specified but three
values are returned. This works because 'by' is processed in a round robin fashion if
there are more values than 'by' modulators.

[source,groovy]
----
// Where can I fly to from DFW that is more than 4,000 miles away?
g.V().has('code','DFW').outE('route').has('dist',gt(4000)).inV().
                        path().by('code').by('dist')
----

Here are the results of running the query.

[source,groovy]
----
[DFW,4736,LHR]
[DFW,4933,CDG]
[DFW,5127,FRA]
[DFW,5208,HEL]
[DFW,6410,NRT]
[DFW,8574,SYD]
[DFW,8022,DXB]
[DFW,4458,DUB]
[DFW,8105,HKG]
[DFW,6951,PEK]
[DFW,7332,PVG]
[DFW,5597,FCO]
[DFW,4905,AMS]
[DFW,4950,MAD]
[DFW,5313,MUC]
[DFW,7914,DOH]
[DFW,6822,ICN]
[DFW,5228,GIG]
[DFW,5119,GRU]
[DFW,5299,EZE]
[DFW,4884,SCL]
[DFW,6257,IST]
[DFW,8053,AUH]
[DFW,5015,DUS]
----

The previous results are not sorted in any way. We could modify the query to include
an 'order' step so that the results are sorted in descending order by distance.

[source,groovy]
----
g.V().has('code','DFW').outE('route').has('dist',gt(4000)).
      order().by('dist',desc).inV().
      path().by('code').by('dist')
----

Here are the, now sorted, results.

[source,groovy]
----
[DFW,8574,SYD]
[DFW,8105,HKG]
[DFW,8053,AUH]
[DFW,8022,DXB]
[DFW,7914,DOH]
[DFW,7332,PVG]
[DFW,6951,PEK]
[DFW,6822,ICN]
[DFW,6410,NRT]
[DFW,6257,IST]
[DFW,5597,FCO]
[DFW,5313,MUC]
[DFW,5299,EZE]
[DFW,5228,GIG]
[DFW,5208,HEL]
[DFW,5127,FRA]
[DFW,5119,GRU]
[DFW,5015,DUS]
[DFW,4950,MAD]
[DFW,4933,CDG]
[DFW,4905,AMS]
[DFW,4884,SCL]
[DFW,4736,LHR]
[DFW,4458,DUB]
----

This next query also finds all routes longer than 4,000 miles but this time
originating in London Gatwick. Note also the use of 'where' to query the edge
distance. The 'has' form is simpler but we show 'where' being used just to
demonstrate an alternative way we could do it. Note that this query uses three 'by' 
modulators as each of the values returned is from a different property of the 
respective vertices and edges.

[source,groovy]
----
// Routes longer than 4,000 miles starting at LGW
g.V().has('code','LGW').outE().where(values('dist').is(gt(4000L))).
      inV().path().by('code').by('dist').by('city')
----

Here are the results from running the query.

[source,groovy]
----
[LGW,4921,Austin]                [LGW,4410,Fort Lauderdale]
[LGW,5463,Los Angeles]           [LGW,4341,Orlando]
[LGW,4429,Miami]                 [LGW,4807,Seattle]
[LGW,5374,San Francisco]         [LGW,4416,Tampa]
[LGW,5236,Las Vegas]             [LGW,4678,Denver]
[LGW,5364,Oakland]               [LGW,4731,Vancouver]
[LGW,6751,Singapore]             [LGW,5982,Hong Kong]
[LGW,5070,Beijing]               [LGW,5745,Shanghai]
[LGW,4380,Calgary]               [LGW,5987,Cape Town]
[LGW,5736,Rio de Janeiro]        [LGW,6908,Buenos Aires]
[LGW,4680,Kingston]              [LGW,4953,Cancun]
[LGW,5399,Colombo]               [LGW,6080,Taipei]
[LGW,4197,Bridgetown]            [LGW,4076,St. George]
[LGW,4408,Port of Spain]         [LGW,4699,Montego Bay]
[LGW,4283,Punta Cana]            [LGW,5419,San Jose]
[LGW,4662,Havana]                [LGW,5156,Chengdu]
[LGW,6053,Port Louis]            [LGW,4222,Vieux Fort]
[LGW,5287,MalÃ©]                  [LGW,4077,Kigali]
[LGW,4618,Varadero]              [LGW,5147,Tianjin]
[LGW,5303,Chongqing]             [LGW,6299,Langkawi]
[LGW,6008,Rayong]                [LGW,6264,Duong Dong]
----

This next query is similar to the previous ones. We look for any routes from DFW that
are longer than 4,500 miles. However, there are a few differences in this query
worthy of note. First of all it uses the preferred 'has' technique again to test the
distance whereas in the previous query we used 'where'. Also this time we just list
the distance and the destination airport's code and we sort the end result using a
'sort'. We also use 'select' and 'as' rather than the perhaps more succinct 'path'
and 'by' to show a different way of achieving effectively the same results. The
'path' and 'by' combination were introduced more recently into TinkerPop and we find
that to be a more convenient syntax to use most of the time but both ways work and
both have their benefits. We should also note that we show 'sort' being used here
just to show a different way of ordering results, but in most cases we can re-write
our query to use 'order'. This use of 'sort' requires a Groovy closure to be
provided. This may not work when working with commercial graph databases that
disallow closures. You will find several examples of 'order' being used throughout
this book. The 'order' step is introduced in the "<<sort>>" section. As much as
possible staying with the pure Gremlin syntax and avoiding closures is recommended.

[source,groovy]
----
// Routes from DFW that are over 4,500 miles in length.
// Sorted into ascending order
g.V().hasLabel('airport').has('code','DFW').outE().has('dist',gt(4500)).as('e').
      inV().as('c').select('e','c').by('dist').by('code').sort(){it.e}
----

In general, we find using 'path' rather than 'select' and 'as' to be cleaner.
However, as discussed in the "<<pathwarn>>" section, there are issues with 'path'
consuming memory that you will likely run into with more complex queries so it is
always good to have some options as to how you write your Gremlin queries. Here is
the output produced by this query.

[source,groovy]
----
[e:4736,c:LHR]
[e:4884,c:SCL]
[e:4905,c:AMS]
[e:4933,c:CDG]
[e:4950,c:MAD]
[e:5015,c:DUS]
[e:5119,c:GRU]
[e:5127,c:FRA]
[e:5208,c:HEL]
[e:5228,c:GIG]
[e:5299,c:EZE]
[e:5313,c:MUC]
[e:5597,c:FCO]
[e:6257,c:IST]
[e:6410,c:NRT]
[e:6822,c:ICN]
[e:6951,c:PEK]
[e:7332,c:PVG]
[e:7914,c:DOH]
[e:8022,c:DXB]
[e:8053,c:AUH]
[e:8105,c:HKG]
[e:8574,c:SYD]
----

For the sake of completeness, here is the query re-written to use an 'order' step
rather than a call to 'sort' as a post processing step at the end of the query. In
general this is the recommended way of achieving sorted results.

[source,groovy]
----
g.V().hasLabel('airport').has('code','DFW').outE().has('dist',gt(4500)).
      order().by('dist').as('e').inV().as('c').
      select('e','c').by('dist').by('code')
----

Note that the results are ordered before being collected using the 'select' step.
Here is the output from running the modified query

[source,groovy]
----
[e:4736,c:LHR]
[e:4884,c:SCL]
[e:4905,c:AMS]
[e:4933,c:CDG]
[e:4950,c:MAD]
[e:5015,c:DUS]
[e:5119,c:GRU]
[e:5127,c:FRA]
[e:5208,c:HEL]
[e:5228,c:GIG]
[e:5299,c:EZE]
[e:5313,c:MUC]
[e:5597,c:FCO]
[e:6257,c:IST]
[e:6410,c:NRT]
[e:6822,c:ICN]
[e:6951,c:PEK]
[e:7332,c:PVG]
[e:7914,c:DOH]
[e:8022,c:DXB]
[e:8053,c:AUH]
[e:8105,c:HKG]
[e:8574,c:SYD]
----

The query can also be written with the 'order' step coming after the 'select' step.
As follows:

[source,groovy]
----
g.V().hasLabel('airport').has('code','DFW').outE().has('dist',gt(4500)).as('e').
  inV().as('c').select('e','c').by('dist').by('code').order().by(select('e'))
----

[[gt8k]]
==== Finding routes longer than 8,000 miles

This next set of queries show various ways of finding and presenting all routes
longer than 8,000 miles. Each query improves upon the one before by adding some
additional feature or using a step that simplifies the query. First of all let's just
find all routes longer than 8,000 miles. This will includes routes in both directions
between airport pairs.

[source,groovy]
----
// All routes longer than 8,000 miles
g.V().as('src').outE('route').
      has('dist',gt(8000)).inV().as('dest').
      select('src','dest').by('code')
----

Here is what our query produces. As before we have arranged the output in columns to
aid readability.

[source,groovy]
----
[src:ATL,dest:JNB]    [src:DFW,dest:SYD]    [src:DFW,dest:DXB]
[src:DFW,dest:HKG]    [src:DFW,dest:AUH]    [src:IAD,dest:HKG]
[src:IAH,dest:SYD]    [src:IAH,dest:DXB]    [src:IAH,dest:DOH]
[src:JFK,dest:SIN]    [src:JFK,dest:HKG]    [src:JFK,dest:MNL]
[src:LAX,dest:SIN]    [src:LAX,dest:DXB]    [src:LAX,dest:DOH]
[src:LAX,dest:AUH]    [src:LAX,dest:JED]    [src:LAX,dest:RUH]
[src:ORD,dest:AKL]    [src:SEA,dest:SIN]    [src:SFO,dest:SIN]
[src:SFO,dest:DXB]    [src:SFO,dest:AUH]    [src:EWR,dest:SIN]
[src:EWR,dest:HKG]    [src:YVR,dest:MEL]    [src:LHR,dest:PER]
[src:SYD,dest:DFW]    [src:SYD,dest:IAH]    [src:SIN,dest:JFK]
[src:SIN,dest:LAX]    [src:SIN,dest:SEA]    [src:SIN,dest:SFO]
[src:SIN,dest:EWR]    [src:MEL,dest:YVR]    [src:DXB,dest:DFW]
[src:DXB,dest:IAH]    [src:DXB,dest:LAX]    [src:DXB,dest:SFO]
[src:DXB,dest:AKL]    [src:HKG,dest:DFW]    [src:HKG,dest:IAD]
[src:HKG,dest:JFK]    [src:HKG,dest:EWR]    [src:PER,dest:LHR]
[src:AKL,dest:ORD]    [src:AKL,dest:DXB]    [src:AKL,dest:DOH]
[src:PEK,dest:PTY]    [src:MNL,dest:JFK]    [src:DOH,dest:IAH]
[src:DOH,dest:LAX]    [src:DOH,dest:AKL]    [src:JNB,dest:ATL]
[src:SCL,dest:TLV]    [src:MEX,dest:CAN]    [src:TLV,dest:SCL]
[src:AUH,dest:DFW]    [src:AUH,dest:LAX]    [src:AUH,dest:SFO]
[src:JED,dest:LAX]    [src:RUH,dest:LAX]    [src:CAN,dest:MEX]
[src:PTY,dest:PEK]
----

Now let's improve the query by including the distance of each route in the query
results.

[source,groovy]
----
// Find routes longer than 8,000 miles.
// Include the distance in returned values.
g.V().as('src').
      outE().has('dist',gt(8000)).as('e').
      inV().as('dest').
      select('src','e','dest').by('code').by('dist')
----

Here is the modified output showing the distance between the airports.

[source,groovy]
----
[src:ATL,e:8434,dest:JNB]         [src:DFW,e:8574,dest:SYD]         [src:DFW,e:8022,dest:DXB]
[src:DFW,e:8105,dest:HKG]         [src:DFW,e:8053,dest:AUH]         [src:IAD,e:8135,dest:HKG]
[src:IAH,e:8591,dest:SYD]         [src:IAH,e:8150,dest:DXB]         [src:IAH,e:8030,dest:DOH]
[src:JFK,e:9526,dest:SIN]         [src:JFK,e:8054,dest:HKG]         [src:JFK,e:8504,dest:MNL]
[src:LAX,e:8756,dest:SIN]         [src:LAX,e:8321,dest:DXB]         [src:LAX,e:8287,dest:DOH]
[src:LAX,e:8372,dest:AUH]         [src:LAX,e:8314,dest:JED]         [src:LAX,e:8246,dest:RUH]
[src:ORD,e:8186,dest:AKL]         [src:SEA,e:8059,dest:SIN]         [src:SFO,e:8433,dest:SIN]
[src:SFO,e:8085,dest:DXB]         [src:SFO,e:8139,dest:AUH]         [src:EWR,e:9523,dest:SIN]
[src:EWR,e:8047,dest:HKG]         [src:YVR,e:8197,dest:MEL]         [src:LHR,e:9009,dest:PER]
[src:SYD,e:8574,dest:DFW]         [src:SYD,e:8591,dest:IAH]         [src:SIN,e:9526,dest:JFK]
[src:SIN,e:8756,dest:LAX]         [src:SIN,e:8059,dest:SEA]         [src:SIN,e:8433,dest:SFO]
[src:SIN,e:9523,dest:EWR]         [src:MEL,e:8197,dest:YVR]         [src:DXB,e:8022,dest:DFW]
[src:DXB,e:8150,dest:IAH]         [src:DXB,e:8321,dest:LAX]         [src:DXB,e:8085,dest:SFO]
[src:DXB,e:8818,dest:AKL]         [src:HKG,e:8105,dest:DFW]         [src:HKG,e:8135,dest:IAD]
[src:HKG,e:8054,dest:JFK]         [src:HKG,e:8047,dest:EWR]         [src:PER,e:9009,dest:LHR]
[src:AKL,e:8186,dest:ORD]         [src:AKL,e:8818,dest:DXB]         [src:AKL,e:9025,dest:DOH]
[src:PEK,e:8884,dest:PTY]         [src:MNL,e:8504,dest:JFK]         [src:DOH,e:8030,dest:IAH]
[src:DOH,e:8287,dest:LAX]         [src:DOH,e:9025,dest:AKL]         [src:JNB,e:8434,dest:ATL]
[src:SCL,e:8208,dest:TLV]         [src:MEX,e:8754,dest:CAN]         [src:TLV,e:8208,dest:SCL]
[src:AUH,e:8053,dest:DFW]         [src:AUH,e:8372,dest:LAX]         [src:AUH,e:8139,dest:SFO]
[src:JED,e:8314,dest:LAX]         [src:RUH,e:8246,dest:LAX]         [src:CAN,e:8754,dest:MEX]
[src:PTY,e:8884,dest:PEK]
----

Next let's simplify things a bit. While using 'as' and 'select' gets the job done,
using 'path' and 'by' shortens the query and makes it more readable. As before
remember the warning that in some cases using 'path' can consume large amounts of
memory. That should not be an issue for us here as we are writing a fairly simple
query still.

[source,groovy]
----
// Note that this also changes the way the result is returned
g.V().outE().has('dist',gt(8000)).
       inV().path().by('code').by('dist')
----

The output indeed now looks more readable.

[source,groovy]
----
[ATL,8434,JNB]  [DFW,8574,SYD]  [DFW,8022,DXB]
[DFW,8105,HKG]  [DFW,8053,AUH]  [IAD,8135,HKG]
[IAH,8591,SYD]  [IAH,8150,DXB]  [IAH,8030,DOH]
[JFK,9526,SIN]  [JFK,8054,HKG]  [JFK,8504,MNL]
[LAX,8756,SIN]  [LAX,8321,DXB]  [LAX,8287,DOH]
[LAX,8372,AUH]  [LAX,8314,JED]  [LAX,8246,RUH]
[ORD,8186,AKL]  [SEA,8059,SIN]  [SFO,8433,SIN]
[SFO,8085,DXB]  [SFO,8139,AUH]  [EWR,9523,SIN]
[EWR,8047,HKG]  [YVR,8197,MEL]  [LHR,9009,PER]
[SYD,8574,DFW]  [SYD,8591,IAH]  [SIN,9526,JFK]
[SIN,8756,LAX]  [SIN,8059,SEA]  [SIN,8433,SFO]
[SIN,9523,EWR]  [MEL,8197,YVR]  [DXB,8022,DFW]
[DXB,8150,IAH]  [DXB,8321,LAX]  [DXB,8085,SFO]
[DXB,8818,AKL]  [HKG,8105,DFW]  [HKG,8135,IAD]
[HKG,8054,JFK]  [HKG,8047,EWR]  [PER,9009,LHR]
[AKL,8186,ORD]  [AKL,8818,DXB]  [AKL,9025,DOH]
[PEK,8884,PTY]  [MNL,8504,JFK]  [DOH,8030,IAH]
[DOH,8287,LAX]  [DOH,9025,AKL]  [JNB,8434,ATL]
[SCL,8208,TLV]  [MEX,8754,CAN]  [TLV,8208,SCL]
[AUH,8053,DFW]  [AUH,8372,LAX]  [AUH,8139,SFO]
[JED,8314,LAX]  [RUH,8246,LAX]  [CAN,8754,MEX]
[PTY,8884,PEK]
----

Our query is looking pretty good but it would be nice to not report the same route
pair twice. In other words the distance between two airports in just one direction
is all we really want. This adds a little complexity to things as we have to find a
way to 'filter' out the routes that we want to ignore. One way to do this is to
filter by making sure the code for the source airport is less than the code for the
destination airport. This may seem a bit odd but it is a way of saying we only want
route pairs we have not already seen. Consider the case of the route between ATL and
JNB. The less than ('lt') test works as we will allow the route between ATL and JNB
through (as ATL is alphabetically less than JNB) but the route between JNB and ATL
will be filtered out. This is a useful technique that can be very helpful in many
situations where you are filtering out unwanted results. So let's modify the query as
shown below to apply this filter.

[source,groovy]
----
// We could avoid returning both directions of
// travel by refining our query as follows.
g.V().as('a').outE().has('dist',gt(8000)).
      inV().as('b').
      filter(select('a','b').by('code').where('a', lt('b'))).
      path().by('code').by('dist')
----

Here is the output from running the modified query. As you can see the route from ATL
to JNB is still shown but the reverse route from JNB to ATL is now gone. The same is
true for all the other 'return journey' routes.

[source,groovy]
----
[ATL,8434,JNB]
[DFW,8574,SYD]
[DFW,8022,DXB]
[DFW,8105,HKG]
[IAH,8591,SYD]
[JFK,9526,SIN]
[JFK,8504,MNL]
[LAX,8756,SIN]
[LAX,8246,RUH]
[SEA,8059,SIN]
[SFO,8433,SIN]
[EWR,9523,SIN]
[EWR,8047,HKG]
[LHR,9009,PER]
[MEL,8197,YVR]
[DXB,8150,IAH]
[DXB,8321,LAX]
[DXB,8085,SFO]
[HKG,8135,IAD]
[HKG,8054,JFK]
[AKL,8186,ORD]
[AKL,8818,DXB]
[AKL,9025,DOH]
[PEK,8884,PTY]
[DOH,8030,IAH]
[DOH,8287,LAX]
[SCL,8208,TLV]
[AUH,8053,DFW]
[AUH,8372,LAX]
[AUH,8139,SFO]
[JED,8314,LAX]
[CAN,8754,MEX]
----

Lastly, now that we have the routes we want, let's tweak the query so that the routes
are sorted by descending order of distance. We can do this by adding an 'order'
step after finding the routes we are interested in.

[source,groovy]
----
// As above but sorted by route lengths.
g.V().as('a').outE().has('dist',gt(8000)).
      order().by('dist',desc).
      inV().as('b').
      filter(select('a','b').by('code').where('a', lt('b'))).
      path().by('code').by('dist')
----

Here are the results again, this time sorted by route distance in descending order.

[source,groovy]
----
[JFK,9526,SIN]
[EWR,9523,SIN]
[AKL,9025,DOH]
[LHR,9009,PER]
[PEK,8884,PTY]
[AKL,8818,DXB]
[LAX,8756,SIN]
[CAN,8754,MEX]
[IAH,8591,SYD]
[DFW,8574,SYD]
[JFK,8504,MNL]
[ATL,8434,JNB]
[SFO,8433,SIN]
[AUH,8372,LAX]
[DXB,8321,LAX]
[JED,8314,LAX]
[DOH,8287,LAX]
[LAX,8246,RUH]
[SCL,8208,TLV]
[MEL,8197,YVR]
[AKL,8186,ORD]
[DXB,8150,IAH]
[AUH,8139,SFO]
[HKG,8135,IAD]
[DFW,8105,HKG]
[DXB,8085,SFO]
[SEA,8059,SIN]
[HKG,8054,JFK]
[AUH,8053,DFW]
[EWR,8047,HKG]
[DOH,8030,IAH]
[DFW,8022,DXB]
----

The 'where' step can be followed by a 'by' modulator. This allows us, should we so
desire, to simplify our query a bit more as follows.

[source,groovy]
----
//Query changed to take advantage of the where().by() construct
g.V().as('s').
      outE().has('dist',gt(8000)).
      order().by('dist',desc).inV().as('f').
      where('f',lt('s')).by('code').
      path().by('code').by('dist')
----

As you can see, we got the same results from our modified query. Well, almost the
same results! Notice that we actually got the 'return routes' returned. So for
example, we got the route from DXB to AKL and not the route from AKL to DXB that
we got in the prior case. This is because we compared the values in the opposite
order!

[source,groovy]
----
[SIN,9526,JFK]
[SIN,9523,EWR]
[DOH,9025,AKL]
[PER,9009,LHR]
[PTY,8884,PEK]
[DXB,8818,AKL]
[SIN,8756,LAX]
[MEX,8754,CAN]
[SYD,8591,IAH]
[SYD,8574,DFW]
[MNL,8504,JFK]
[JNB,8434,ATL]
[SIN,8433,SFO]
[LAX,8372,AUH]
[LAX,8321,DXB]
[LAX,8314,JED]
[LAX,8287,DOH]
[RUH,8246,LAX]
[TLV,8208,SCL]
[YVR,8197,MEL]
[ORD,8186,AKL]
[IAH,8150,DXB]
[SFO,8139,AUH]
[IAD,8135,HKG]
[HKG,8105,DFW]
[SFO,8085,DXB]
[SIN,8059,SEA]
[JFK,8054,HKG]
[DFW,8053,AUH]
[HKG,8047,EWR]
[IAH,8030,DOH]
[DXB,8022,DFW]
----

As an interesting side note, and this would be true for the queries above as well, if
we replace the 'lt' with a 'gt' we will get the routes returned in the reverse order.


[source,groovy]
----
g.V().as('s').
      outE().has('dist',gt(8000)).
      order().by('dist',desc).inV().as('f').
      where('f',gt('s')).by('code').
      path().by('code').by('dist')
----

Using the prior query the first result was '[DOH,9025,AKL]'. As you will
see below, our first result is now '[AKL,9025,DOH]'.

[source,groovy]
----
[JFK,9526,SIN]
[EWR,9523,SIN]
[AKL,9025,DOH]
[LHR,9009,PER]
[PEK,8884,PTY]
[AKL,8818,DXB]
[LAX,8756,SIN]
[CAN,8754,MEX]
[IAH,8591,SYD]
[DFW,8574,SYD]
[JFK,8504,MNL]
[ATL,8434,JNB]
[SFO,8433,SIN]
[AUH,8372,LAX]
[DXB,8321,LAX]
[JED,8314,LAX]
[DOH,8287,LAX]
[LAX,8246,RUH]
[SCL,8208,TLV]
[MEL,8197,YVR]
[AKL,8186,ORD]
[DXB,8150,IAH]
[AUH,8139,SFO]
[HKG,8135,IAD]
[DFW,8105,HKG]
[DXB,8085,SFO]
[SEA,8059,SIN]
[HKG,8054,JFK]
[AUH,8053,DFW]
[EWR,8047,HKG]
[DOH,8030,IAH]
[DFW,8022,DXB]
----

==== Finding the 20 longest routes in the graph

We could write the query from the previous section a different way looking at edges
first and using the 'project' step instead of using the 'as' and 'select' steps.
While in general it is not recommended to start a query with 'g.E()' as there are
typically a lot more edges than vertices in a graph this does illustrate a useful
pattern. Also, just to change things a bit, this time we just look for the 20 longest
routes rather than looking for routes longer than 8,000 miles. We again filter the
results to only show the route in one direction. Note that the limit step is passed a
parameter of 40 rather than 20 as we know we will be filtering out the same route in
the return direction so we will actually only get 20 routes back.

[source,groovy]
----
g.E().hasLabel('route').
      order().by('dist',desc).limit(40).
      project('a','b','c').
        by(inV().values('code')).
        by('dist').
        by(outV().values('code')).
      filter(select('a','c')).where('a',lt('c'))
----

Here are the results from running the query.

[source,groovy]
----
[a:JFK,b:9526,c:SIN]
[a:EWR,b:9523,c:SIN]
[a:AKL,b:9025,c:DOH]
[a:LHR,b:9009,c:PER]
[a:PEK,b:8884,c:PTY]
[a:AKL,b:8818,c:DXB]
[a:LAX,b:8756,c:SIN]
[a:CAN,b:8754,c:MEX]
[a:IAH,b:8591,c:SYD]
[a:DFW,b:8574,c:SYD]
[a:JFK,b:8504,c:MNL]
[a:ATL,b:8434,c:JNB]
[a:SFO,b:8433,c:SIN]
[a:AUH,b:8372,c:LAX]
[a:DXB,b:8321,c:LAX]
[a:JED,b:8314,c:LAX]
[a:DOH,b:8287,c:LAX]
[a:LAX,b:8246,c:RUH]
[a:SCL,b:8208,c:TLV]
[a:MEL,b:8197,c:YVR]
----

For completeness, here is the query rewritten slightly to again start with airport
vertices rather than with all 'route' edges but using 'as' and 'select' steps rather
than using a 'path' step as used in the previous section. A 'where' step followed by
a 'by' modulator is still used in this case. So once again, it is clear there are
often many ways to get the results that you are looking for. The key is to think
about which form of a query will be the most effective for the data that you are
working with.

[source,groovy]
----
g.V().hasLabel('airport').as('a').
      outE().as('b').
      order().by('dist',desc).limit(40).
      inV().as('c').
      where('a',lt('c')).by('code').
      select('a','b','c').by('code').by('dist')
----

When the query is run the results look like the ones from the prior query.

[source,groovy]
----
[a:JFK,b:9526,c:SIN]
[a:EWR,b:9523,c:SIN]
[a:AKL,b:9025,c:DOH]
[a:LHR,b:9009,c:PER]
[a:PEK,b:8884,c:PTY]
[a:AKL,b:8818,c:DXB]
[a:LAX,b:8756,c:SIN]
[a:CAN,b:8754,c:MEX]
[a:IAH,b:8591,c:SYD]
[a:DFW,b:8574,c:SYD]
[a:JFK,b:8504,c:MNL]
[a:ATL,b:8434,c:JNB]
[a:SFO,b:8433,c:SIN]
[a:AUH,b:8372,c:LAX]
[a:DXB,b:8321,c:LAX]
[a:JED,b:8314,c:LAX]
[a:DOH,b:8287,c:LAX]
[a:LAX,b:8246,c:RUH]
[a:SCL,b:8208,c:TLV]
[a:MEL,b:8197,c:YVR]
----

[[longesteach]]
==== Finding the longest route from each airport

The following query can be used to find the longest route from each airport in
the graph. We included a 'limit' step so that just a few results are returned but
if you were to remove it then every airport in the graph would be represented in
the query results. A local step is used so that for each airport all of its
outgoing routes are analyzed. Those routes are ordered in descending order and
only the first one is selected in each case.

[source,groovy]
----
g.V().hasLabel('airport').limit(10).
      local(outE().
            order().by('dist',desc).
            inV().
            path().
              by('code').
              by('dist').
              limit(1))
----

When run the query produces the following results.

[source,groovy]
----
[ATL,8434,JNB]
[ANC,3368,KEF]
[AUS,5294,FRA]
[BNA,4168,LHR]
[BOS,7952,HKG]
[BWI,3622,LHR]
[DCA,2434,SFO]
[DFW,8574,SYD]
[FLL,7808,DXB]
[IAD,8135,HKG]
----

[[aggrunion]]
==== Combining 'aggregate', 'union' and 'filter' to compute distances

This next query is similar to the one we looked at in the "<<aggrroutes>>" section.
It uses 'aggregate', 'union', 'filter' and 'where' to factor certain airports in and
out of a query. We find all airports in London, Munich and Paris and then count the
total distance of all routes from those airports as the first half of a 'union'. The
second half of the 'union' only counts the distances of routes that end up in one of
our three selected cities.

[source,groovy]
----
g.V().has('city',within('London','Munich','Paris')).
      aggregate('a').
      outE().
      union(values('dist').sum(),
            filter(inV().where((within('a')))).
              values('dist').sum())
----

Here are the results from running the query. As expected the first number is a
lot bigger than the second one as it is the total distance of all routes from
the selected airports whereas the second number only reflects the total
distance of all routes between those airports.

[source,groovy]
----
2739039
10282
----

==== More queries that analyze distances

Calculating the distance between two directly connected airports is very easy. All we
have to do is look at the 'dist' property of the edge that connects them. We can do
this using the 'select' and 'as' steps or in more recent versions of TinkerPop we can
use 'path' and 'by'. We prefer the latter technique. Both ways are shown in the
examples below that find the distance between Austin and Mexico City

[source,groovy]
----
// Distance between the AUS and MEX airports
g.V().has('code','AUS').outE().as('e').
      inV().has('code','MEX').
      select('e').values('dist')

748
----

As above but using 'path' and 'by'.

[source,groovy]
----
// Distance between the AUS and MEX airports
g.V().has('code','AUS').outE().
      inV().has('code','MEX').
      path().by('code').by('dist')

[AUS, 748, MEX]
----

Here are some more queries that are based on the distance between airports. The first
query calculates how many routes there are between 100 and 200 miles. As an exercise,
If you remove the call to 'count' the query will list the routes. As you can see
there are a lot of them!

[source,groovy]
----
// Routes Between 100 and 200 miles in length
g.V().outE().has('dist',within(100..200)).
      inV().
      path().by('code').by('dist').
      count()

3237
----

The next query is similar to the previous one but only counts routes that are between
airports located in the United States. As before, if you remove the call to 'count'
the routes will be returned.

[source,groovy]
----
// Routes Between 100 and 200 miles in length, but only within the US.
g.V().has('airport','country','US').
      outE().has('dist',within(100..200)).
      inV().has('country','US').
      path().by('code').by('dist').
      count()

597
----

Lastly, this query returns a list of all the routes from San Antonio along with their
distances. Some of the results are shown.

[source,groovy]
----
// Return a list of routes and their distances,
// starting from San Antonio (SAT)
g.V().has('code','SAT').
  outE('route').inV().
  path().by('code').by('dist')
----

The results of this query are as follows:

[source,groovy]
----
[SAT,1423,YYZ]  [SAT,698,MEX]   [SAT,1712,PDX]
[SAT,408,OKC]   [SAT,1163,ONT]  [SAT,1093,CLT]
[SAT,931,CUN]   [SAT,624,MEM]   [SAT,1023,CVG]
[SAT,707,MCI]   [SAT,248,DAL]   [SAT,786,STL]
[SAT,608,ABQ]   [SAT,1036,MDW]  [SAT,825,OMA]
[SAT,484,TUL]   [SAT,1005,JAX]  [SAT,732,COS]
[SAT,234,HRL]   [SAT,1138,CMH]  [SAT,135,CRP]
[SAT,278,MTY]   [SAT,692,GDL]   [SAT,1039,SFB]
[SAT,707,TLC]   [SAT,872,ATL]   [SAT,66,AUS]
[SAT,820,BNA]   [SAT,1410,BWI]  [SAT,248,DFW]
[SAT,1143,FLL]  [SAT,1360,IAD]  [SAT,190,IAH]
[SAT,1580,JFK]  [SAT,1210,LAX]  [SAT,1038,MCO]
[SAT,1140,MIA]  [SAT,1097,MSP]  [SAT,1040,ORD]
[SAT,841,PHX]   [SAT,1222,RDU]  [SAT,1772,SEA]
[SAT,1480,SFO]  [SAT,1451,SJC]  [SAT,970,TPA]
[SAT,1129,SAN]  [SAT,1174,SNA]  [SAT,1086,SLC]
[SAT,1069,LAS]  [SAT,795,DEN]   [SAT,493,MSY]
[SAT,1569,EWR]  [SAT,192,HOU]   [SAT,495,ELP]
[SAT,1239,CLE]  [SAT,1473,OAK]  [SAT,1490,PHL]
[SAT,1210,DTW]
----

[[AUSLHR-OS]]
==== How far is it from AUS to LHR with one stop?

The next query begins to address the question "How far is it from AUS to LHR with one
stop?". We can quite easily come up with a query given what we now know about Gremlin
that will show us all possible options with one stop between AUS and LHR along with
the respective route distances.

[source,groovy]
----
// Return all ways of getting from AUS to LHR with one stop.
// Include the distances between each of the airports in the
// query result.
g.V().has('airport','code','AUS').
      outE().inV().outE().inV().
      has('code','LHR').
      path().by('code').by('dist')
----

The output from running the query produces a nice set of data showing the starting,
intermediate and destination airports with the distances in miles between each. It
would be nice though if we could find a way to show the total distance that we will
have to travel in each case. That is the topic of the next section!

[source,groovy]
----
[AUS,1430,PHL,3533,LHR]         [AUS,1712,PDX,4896,LHR]
[AUS,1140,DTW,3753,LHR]         [AUS,1030,CLT,3980,LHR]
[AUS,1284,NAS,4332,LHR]         [AUS,1053,CHS,4053,LHR]
[AUS,809,ATL,4198,LHR]          [AUS,755,BNA,4168,LHR]
[AUS,1690,BOS,3254,LHR]         [AUS,1339,BWI,3622,LHR]
[AUS,190,DFW,4736,LHR]          [AUS,1294,IAD,3665,LHR]
[AUS,142,IAH,4820,LHR]          [AUS,1520,JFK,3440,LHR]
[AUS,1230,LAX,5439,LHR]         [AUS,1101,MIA,4414,LHR]
[AUS,1040,MSP,4001,LHR]         [AUS,973,ORD,3939,LHR]
[AUS,866,PHX,5255,LHR]          [AUS,1159,RDU,3860,LHR]
[AUS,1768,SEA,4783,LHR]         [AUS,1500,SFO,5350,LHR]
[AUS,1476,SJC,5352,LHR]         [AUS,1160,SAN,5469,LHR]
[AUS,1080,SLC,4850,LHR]         [AUS,1080,LAS,5213,LHR]
[AUS,768,DEN,4655,LHR]          [AUS,1357,YYZ,3544,LHR]
[AUS,1869,YVR,4706,LHR]         [AUS,444,MSY,4616,LHR]
[AUS,1500,EWR,3453,LHR]         [AUS,5294,FRA,406,LHR]
[AUS,5074,AMS,230,LHR]          [AUS,1671,YYC,4356,LHR]
[AUS,748,MEX,5529,LHR]          [AUS,1209,PIT,3707,LHR]
----

[[sackauslhr]]
==== Using 'sack' to calculate the shortest AUS-LHR route with one stop

Consider a typical use case for air travel. We want to calculate the shortest
distance we will have to travel to go from one airport to another with only one stop.
Let's take a real example. What are the ten shortest distances from AUS to LHR with
one stop on the way?  Given what we know of Gremlin so far we can pretty easily come
up with a query that will give us routes from AUS to LHR with just one stop like we
did in the previous section. However, what is not so obvious, and this is an area
where the TinkerPop documentation is a little weak, is working out how to keep a
running total of values as we traverse a graph. This is where the 'sack' step comes
in. As you will hopefully recall from some of the earlier sections, a 'sack' allows
us to define a place where we can put things as the graph traversal proceeds. We can
give the sack an in initial value and can add to it during the traversal and then use
it as part of the information that our query will return. Take a look at the
rather lengthy query below. We have laid it out in a way that we hope makes it easy
to read.

[source,groovy]
----
// Shortest distances from AUS to LHR with one stop
g.withSack(0).
  V().has('code','AUS').
      outE().
      sack(sum).by('dist').
      inV().
      outE().
      sack(sum).by('dist').
      inV().has('code','LHR').
      sack().
      order().by(asc).limit(10).
      path().
        by('code').
        by('dist').
        by('code').
        by('dist').
        by('code').
        by()
----

On the first line of the query, we initialize our sack with the value zero. During
the query, each time we take an outgoing edge, we add the distance value for that
edge to the sack. We filter out routes in the normal way by only keeping destinations
that are 'LHR'. After finding the routes we care about we take the values that are
stored in our sack and use them to sort our results in ascending order. Finally on
we process the paths that we have taken. Note that the sack value is included
as part of the path output and referenced using a 'by' modulator that has no
parameter. Running the query will produce the following results:

[source,groovy]
----
[AUS,1140,DTW,3753,LHR,4893]
[AUS,1357,YYZ,3544,LHR,4901]
[AUS,973,ORD,3939,LHR,4912]
[AUS,1209,PIT,3707,LHR,4916]
[AUS,755,BNA,4168,LHR,4923]
[AUS,190,DFW,4736,LHR,4926]
[AUS,1690,BOS,3254,LHR,4944]
[AUS,1500,EWR,3453,LHR,4953]
[AUS,1294,IAD,3665,LHR,4959]
[AUS,1520,JFK,3440,LHR,4960]
----

We could add a little post processing to our query to only output the airport codes
and the total mileage. Given this is post processing of a small data set, using a bit
of in-line code does not feel too ugly here.

[source,groovy]
----
g.withSack(0).V().
  has('code','AUS').
  outE().
  sack(sum).by('dist').
  inV().
  outE().
  sack(sum).by('dist').
  inV().has('code','LHR').
  sack().
  order().by(asc).limit(10).
  path().
    by('code').
    by('dist').
    by('code').
    by('dist').
    by('code').
    by().
  toList().
  each(){println "${it[0]} --> ${it[2]} --> ${it[4]} ${it[5]} miles"}[];
----

Here is what our modified query, with the Groovy post processing added, produces.

[source,groovy]
----
AUS --> DTW --> LHR 4893 miles
AUS --> YYZ --> LHR 4901 miles
AUS --> ORD --> LHR 4912 miles
AUS --> PIT --> LHR 4916 miles
AUS --> BNA --> LHR 4923 miles
AUS --> DFW --> LHR 4926 miles
AUS --> BOS --> LHR 4944 miles
AUS --> EWR --> LHR 4953 miles
AUS --> IAD --> LHR 4959 miles
AUS --> JFK --> LHR 4960 miles
----

[[anothersack]]
==== Another example of how 'sack' can be used

The query below finds all airports with more than 200 routes and returns them as a
map of airport code and route count pairs.

[source,groovy]
----
// Print a table of airports with more than 200 routes and the number of routes
g.V().hasLabel('airport').
      where(out().count().is(gt(200))).
      group().
        by('code').
        by(outE().count())
----

Here are the results that the query generates.

[source,groovy]
----
[ORD:265,PVG:213,LAX:214,CDG:293,STN:211,JFK:204,DFW:253,LHR:221,MUC:270,DME:232,
 EWR:203,AMS:283,IST:309,DEN:217,BCN:203,BER:202,DXB:248,MAD:206,FCO:201,VIE:206,
 FRA:310,PEK:248,ATL:242,MAN:216,LGW:232]
----

The previous query is a perfectly good way to achieve the desired result. Just for 
fun let's produce the same results using a 'sack'. This is intended just as an 
example of how 'sack' works and is not the way you would actually want to perform 
this specific query. That said, it is useful to have an example where the contents of
the sack is more complex than a simple integer value. At the start of the query we 
initialize our sack with an empty map '[:]'. Later in the query, for each airport 
that has more than 200 outgoing routes, we update the map by adding the airport code 
and the route count to the map. Note that the value part of each map entry is
produced using a traversal. So while this query is most definitely overkill for the 
task (as demonstrated by the much simpler query above) it does provide us with 
another example of how you can use sacks in powerful ways to store data as your 
query iterates.

[source,groovy]
----
// The same query but done using the sack() step in TinkerPop shown as an
// example only. The prior query works just fine for this.
g.withSack([:]).
  V().hasLabel('airport').
  where(out().count().is(gt(200))).
  sack{m,v -> m[v.value('code')]=g.V(v).out().count().next()}.
  fold().sack()
----

Here are the results that the new query generates. Other than the fact that the
results came back in a different order they are the same.

[source,groovy]
----
[ATL:242,DFW:253,JFK:204,LAX:214,ORD:265,DEN:217,EWR:203,LHR:221,LGW:232,CDG:293,
 FRA:310,DXB:248,PEK:248,PVG:213,FCO:201,AMS:283,BCN:203,MAD:206,VIE:206,MUC:270,
 MAN:216,STN:211,DME:232,IST:309,BER:202]
----

The 'fold' step is needed in the query above to make sure that the result we get back
is returned as a map. If we left the 'fold' off we would just get the values stored
in the sack returned as a list of integers. Note that while the list of airports is
the same as the previous query, the order is different. This is a result of the way
the 'group' step did its work in the previous query. Order should never be relied
upon. If you need a specific order for the results of a query it is always
recommended to perform an explicit 'order' step as appropriate.

For completeness, a way of sorting the results of our original query by ascending
route count (values) is shown below.

[source,groovy]
----
g.V().hasLabel('airport').
      where(out().count().is(gt(200))).
      group().by('code').by(outE().count()).
      order(local).by(values)

[FCO:201,BER:202,EWR:203,BCN:203,JFK:204,MAD:206,VIE:206,STN:211,PVG:213,LAX:214,
 MAN:216,DEN:217,LHR:221,DME:232,LGW:232,ATL:242,DXB:248,PEK:248,DFW:253,ORD:265,
 MUC:270,AMS:283,CDG:293,IST:309,FRA:310]
----

If you wanted to sort using the airport codes you could do it as follows. This time
we will sort the results of the 'sack' based query.

[source,groovy]
----
g.withSack([:]).
  V().hasLabel('airport').where(out().count().is(gt(200))).
  sack{m,v -> m[v.value('code')]=g.V(v).out().count().next()}.
  fold().
  sack().
  order(local).by(keys)

[AMS:283,ATL:242,BCN:203,BER:202,CDG:293,DEN:217,DFW:253,DME:232,DXB:248,EWR:203,
 FCO:201,FRA:310,IST:309,JFK:204,LAX:214,LGW:232,LHR:221,MAD:206,MAN:216,MUC:270,
 ORD:265,PEK:248,PVG:213,STN:211,VIE:206]
----

As well as using 'withSack' to initialize a 'sack' you can also use the 'assign'
operator to do it. The query below uses a constant value of 0 to initialize the sack
and then uses the sack to count the number or runways that the airports you can fly
to from Austin have. At the end of the query we perform a 'sum' step against the sack
which will contain a list holding number of runways for each individual airport so we
need to add all of those to get a single grand total.

[source,groovy]
----
g.V().has('code','AUS').
      sack(assign).by(constant(0)).
      out().
      sack(sum).by('runways').
      sack().sum()

306
----

[[latlonmanual]]
=== Using latitude, longitude and geographical region in queries

The 'air-routes' graph stores the geographic coordinates (latitude and longitude) of
each airport as floating point numbers. Some graph systems such as JanusGraph have
geographic coordinate and shape (geospatial) functions built in, but TinkerGraph does
not. Moreover, GraphML does not offer any specific geospatial support. To keep things
simple and flexible, the air routes data set does not assume any specific back end 
capabilities. Having coordinates provided as basic floating point numbers still 
allows us to write some interesting geospatial queries.

So far, we have not taken advantage of these latitude and longitude coordinates in
queries. In this section we have included some queries that perform interesting
geospatial calculations using just the standard Gremlin steps.

Here is a simple query that finds any airports located North of 77 degrees latitude.
Note that a 'where' step could be used instead of 'filter' as they are synonymous in
this case.

[source,groovy]
----
g.V().filter(values('lat').is(gt(77))).valueMap('city','lat')
----

As discussed earlier in the book, you can often just use 'has' steps instead of
'where' or 'filter' steps. We could have written the previous query as follows.

[source,groovy]
----
g.V().has('lat',gt(77)).valueMap('city','lat')
----

It turns out that just two airports in the graph are located that far North as shown
below in the results from running either form of the query.

[source,groovy]
----
[city:[Longyearbyen],lat:[78.2461013793945]]
[city:[Qaanaaq],lat:[77.4886016846]]
----

We could modify our query to look for airports with a latitude value outside of a
provided upper and lower bound thus finding the most Northerly and Southerly airports
with a single query. Here is a simple example of such a query.

[source,groovy]
----
g.V().has('lat',outside(-50,77)).order().by('lat',asc).valueMap('city','lat')
----

Here are the airports found by running the query.

[source,groovy]
----
[city:[Ushuahia],lat:[-54.8433]]
[city:[Rio Grande],lat:[-53.7777]]
[city:[Punta Arenas],lat:[-53.0026016235352]]
[city:[Mount Pleasant],lat:[-51.8227996826172]]
[city:[Stanley],lat:[-51.6856994628906]]
[city:[Puerto Natales],lat:[-51.671501159668]]
[city:[Rio Gallegos],lat:[-51.6089]]
[city:[El Calafate],lat:[-50.2803001404]]
[city:[Qaanaaq],lat:[77.4886016846]]
[city:[Longyearbyen],lat:[78.2461013793945]]
----

Note that while writing the above query it might have seemed appropriate to use a
'without' step with a range such as '-50..77' but that will not work as without looks
for exact matches against the values in the range and the range generated is of the
form '-50,-49,-48' and so on, so the only airports that would get filtered out would
be the ones having a latitude value that exactly matches one of the values generated
by the range. This is why the 'outside' step is so useful in cases like this.

This next query just returns the coordinates for London Heathrow.

[source,groovy]
----
// Query latitude and longitude for LHR
g.V().has('airport','code','LHR').valueMap('lat','lon')

[lon:[-0.461941003799], lat:[51.4706001282]]
----

This next query returns the code, latitude and longitude for all airports in London,
England. Note that because there are other cities in the world also called London,
such as London, Ontario in Canada, we have to take advantage of the region code
'GB-ENG' to only return airports in London, England.

[source,groovy]
----
g.V().has('airport','city','London').has('region','GB-ENG').valueMap('code','lat','lon')

[code:[LHR], lon:[-0.461941003799], lat:[51.4706001282]]
[code:[LGW], lon:[-0.190277993679047], lat:[51.1481018066406]]
[code:[LCY], lon:[0.055278], lat:[51.505278]]
[code:[STN], lon:[0.234999999404], lat:[51.8849983215]]
[code:[LTN], lon:[-0.368333011865616], lat:[51.874698638916]]
----

Now that we know how to query the geographic coordinates, we can write a query to
find out which airports in the graph are very close to the Greenwich Meridian. In
this case we will look for any airports that have a longitude between -0.1 and 0.1

[source,groovy]
----
// Which airports are very close to the Greenwich Meridian ?
g.V().hasLabel('airport').has('lon',between(-0.1,0.1)).valueMap('code','lon')

[code:[LCY], lon:[0.055278]]
[code:[LDE], lon:[-0.006438999902457]]
[code:[LEH], lon:[0.0880559980869293]]
[code:[CDT], lon:[0.0261109992862]]
----

This next query can be used to find out which airports are closest to the equator.

[source,groovy]
----
// Which airports are closest to the Equator ?
g.V().hasLabel('airport').has('lat',between(-0.1,0.1)).valueMap('code','lat')

[code:[EBB], lat:[0.0423859991133213]]
[code:[MDK], lat:[0.0226000007242]]
[code:[KIS], lat:[-0.0861390009522438]]
[code:[MCP], lat:[0.0506640002131]]
[code:[LGQ], lat:[0.0930560007691]]
----

The code below will find all the airports in the geographic area defined by a one
degree box around London Heathrow. Various graph database may offer functionality to
better support this type of query. It's important to be familiar with all of the 
underlying features of your graph database in addition to what TinkerPop offers 
generally so that you can make the best design choice.

[source,groovy]
----
lat = g.V().has('code','LHR').values('lat').next()
lon = g.V().has('code','LHR').values('lon').next()

g.V().hasLabel('airport').where(and(
                          values('lon').is(between(lon-1,lon+1)),
                          values('lat').is(between(lat-1,lat+1)))).
                          valueMap('code','lat','lon')
----

As we have discussed earlier in this book, it is often possible to avoid use of
'and' step by chaining 'has' steps together. The code below is equivalent to the code
above but avoids the use of 'where' and 'and'.

[source,groovy]
----
lat = g.V().has('code','LHR').values('lat').next()
lon = g.V().has('code','LHR').values('lon').next()

g.V().hasLabel('airport').has('lon',between(lon-1,lon+1)).
                          has('lat',between(lat-1,lat+1)).
                          valueMap('code','lat','lon')
----

Here is the output produced by running either of the snippets of code above inside
the Gremlin Console.

[source,groovy]
----
[code:[LHR],lon:[-0.461941003799],lat:[51.4706001282]]
[code:[LGW],lon:[-0.190277993679047],lat:[51.1481018066406]]
[code:[LCY],lon:[0.055278],lat:[51.505278]]
[code:[STN],lon:[0.234999999404],lat:[51.8849983215]]
[code:[LTN],lon:[-0.368333011865616],lat:[51.874698638916]]
[code:[SOU],lon:[-1.35679996013641],lat:[50.9502983093262]]
----

In the "<<tranges>>" section we came up with a query (shown below) to find routes
with one stop between Austin and Las Vegas, using only airports in the United States
or Canada and avoiding PHX and LAX for plane changes.

[source,groovy]
----
g.V().has('airport','code','AUS').out().
      has('country',within('US','CA')).
      has('code',without('PHX','LAX')).out().
      has('code','LAS').path().by('code')
----

Now that we know how to use the longitude and latitude coordinates stored in the air
routes graph, we could for fun, write this query a different way. If you take a look
at the query below you will see we have added a 'where' step. We still check that we
are only looking at airports in the United States or Canada but then, in the 'where'
step, we further limit the airports we want to consider further by saying we are only
interested if their longitude value is less than that of Austin. In other words, we
only want to change planes at an airport that is to the West of Austin. This is
actually an improvement on the previous query that would have returned routes that
included plane changes in New York and Nashville among other places. With our new
query, no airport that is East of Austin will be considered as a place to change
planes.

[source,groovy]
----
// AUS to LAS with one stop but the stop has to be in the US or Canada
// and West of Austin while avoiding PHX and LAX.
g.V().has('airport','code','AUS').as('aus').out().
      has('country',within('US','CA')).
      where(lt('aus')).by('lon').
      has('code',without('PHX','LAX')).out().
      has('code','LAS').path().by('code')
----

Below you will find the output from running the query. If you know your airport codes
you will see that all of these airports are indeed to the West of Austin. We might
want to improve the query even more however, to factor in sensible nearby airports
that are not to the West of Austin. For example, Dallas Fort Worth (DFW) is not
included in the results as it is situated North of Austin but also a little to the
East. We will leave it as an exercise to come up with a refinement to the query so
that Dallas is also included!.

[source,groovy]
----
[AUS,PDX,LAS]
[AUS,ONT,LAS]
[AUS,ABQ,LAS]
[AUS,SMF,LAS]
[AUS,BOI,LAS]
[AUS,LBB,LAS]
[AUS,RNO,LAS]
[AUS,AMA,LAS]
[AUS,BUR,LAS]
[AUS,BZN,LAS]
[AUS,SEA,LAS]
[AUS,SFO,LAS]
[AUS,SJC,LAS]
[AUS,SAN,LAS]
[AUS,LGB,LAS]
[AUS,SNA,LAS]
[AUS,SLC,LAS]
[AUS,DEN,LAS]
[AUS,SAT,LAS]
[AUS,HNL,LAS]
[AUS,YVR,LAS]
[AUS,ELP,LAS]
[AUS,YYC,LAS]
[AUS,OAK,LAS]
[AUS,TUS,LAS]
----

Here is one more example that is similar to the previous one. First of all we store
the longitude of the Dallas (DFW) airport in the variable 'dfw'. Then we use that
variable to find routes to Las Vegas (LAS) from Austin (AUS) that have one stop but
avoid Phoenix (PHX) and Los Angeles (LAX) and only have a plane change either in
Dallas or to the West of Dallas. Note that we use 'lte' and not 'lt' when testing the
longitude value. It we used 'lt' instead that would also rule out Dallas as an
option. This query, as in the prior one, has the effect of ruling out plane changes
at airports further to the East of Austin than Dallas.

[source,groovy]
----
dfw = g.V().has('code','DFW').values('lon').next()

g.V().has('airport','code','AUS').as('aus').out().
      has('country',within('US','CA')).
      has('lon',lte(dfw)).
      has('code',without('PHX','LAX')).out().
      has('code','LAS').path().by('code')
----

Here is what we get back when we run the query. Still lots of choices even if we
avoid LAX and PHX it seems.

[source,groovy]
----
[AUS,PDX,LAS]
[AUS,OKC,LAS]
[AUS,ONT,LAS]
[AUS,ABQ,LAS]
[AUS,SMF,LAS]
[AUS,BOI,LAS]
[AUS,LBB,LAS]
[AUS,RNO,LAS]
[AUS,AMA,LAS]
[AUS,BUR,LAS]
[AUS,BZN,LAS]
[AUS,DFW,LAS]
[AUS,SEA,LAS]
[AUS,SFO,LAS]
[AUS,SJC,LAS]
[AUS,SAN,LAS]
[AUS,LGB,LAS]
[AUS,SNA,LAS]
[AUS,SLC,LAS]
[AUS,DEN,LAS]
[AUS,SAT,LAS]
[AUS,HNL,LAS]
[AUS,YVR,LAS]
[AUS,ELP,LAS]
[AUS,YYC,LAS]
[AUS,OAK,LAS]
[AUS,TUS,LAS]
----

Let's look at one last query that uses the 'region' property, present on all
airport vertices in the graph.

The query below starts at the DFW airport then looks for all routes to airports also
within the United States. Next those airports are grouped by their region code and
airport code. Finally a few states are selected. The query ends with an 'unfold' step
to make the results a bit more readable.

[source,groovy]
----
g.V().has('airport','code','DFW').out().has('country','US').
      group().by('region').by('code').
      select('US-CA','US-TX','US-FL','US-CO','US-IL').unfold()
----

Below you can see the query results. Each of the selected states is listed along with
a list of airports reachable from DFW.

[source,groovy]
----
US-CA=[ONT, PSP, SMF, FAT, BUR, BFL, MRY, SBA, LAX, SFO, SJC, SAN, SNA, OAK]
US-TX=[LBB, HRL, MAF, CRP, ABI, ACT, CLL, BPT, AMA, BRO, GGG, GRK, LRD, MFE, SJT, SPS, TYR, DRT, AUS, IAH, SAT, HOU, ELP]
US-FL=[RSW, TLH, EYW, JAX, ECP, PNS, VPS, SRQ, GNV, FLL, MCO, MIA, PBI, TPA]
US-CO=[COS, DRO, GJT, EGE, HDN, ASE, GUC, MTJ, DEN]
US-IL=[PIA, BMI, CMI, MLI, SPI, ORD]
----

In the next section we will look at a more complicated query that builds upon the
examples above and performs distance calculations using the latitude and longitude
coordinates present on the airport vertices.

[[GreatCircle]]
==== Using the 'math' step to calculate Great Circle distances

If the latitude and longitude of two places on the Earth are known, the approximate
distance between them can be calculated using the Haversine Great Circle Distance
formula. The formula includes some trigonometrical calculations that can be done in
Gremlin using the 'math' step.

NOTE: If you are interested in better understanding the mathematics used by the
Haversine formula, details can be found
https://en.wikipedia.org/wiki/Haversine_formula[here].

In this section we have included two queries. The first calculates the distance
between Austin and Dallas Fort Worth using a somewhat inflexible approach. This
reflects our first attempt to create a query that computes Great Circle distances all
in Gremlin. The airport codes are embedded in the query. Having got that working we
realized a more generic query would be better. The second example allows an arbitrary
source and destination to be specified.

Let's take a look at the queries. At first, the number of steps used may look a bit
daunting, but if you work your way through it section by section it's actually fairly
straightforward. The Haversine formula needs a couple of constant values. These are
injected into the query using 'withSideEffect' steps. The first constant, 'rdeg'
represents one degree in radians. This is the result from dividing PI by 180. The
second constant 'gcmiles' represents the average radius of the Earth in miles. This
allows for the imperfect spherical shape of the Earth where the radius varies closer
to the poles.

In our first attempt at producing a query, we used a 'has' step to find the AUS and
DFW airports and then produced a group with the airport codes being the keys and a
projection of their latitude and longitude being the values. If we take just that
part of the query and run it this is what we get back.

[source,groovy]
----
g.withSideEffect("rdeg", 0.017453293).
  withSideEffect("gcmiles",3956).
  V().
  has('code',within('AUS','DFW')).
  group().
    by('code').
    by(project('lat','lon').
      by('lat').
      by('lon'))

[DFW:[lat:32.896800994873,lon:-97.0380020141602],
 AUS:[lat:30.1944999694824,lon:-97.6698989868164]]
----

The Haversine formula requires us to calculate the differences between the latitude
and longitude of the two coordinates. The next part of the query does that. A project
step 'project('ladiff','lgdiff','lat1','lon1','lat2','lon2')' is used to collect all
the values we need.

[source,groovy]
----
g.withSideEffect("rdeg", 0.017453293).
  withSideEffect("gcmiles",3956).
  V().
  has('code',within('AUS','DFW')).
  group().
    by('code').
    by(project('lat','lon').
      by('lat').
      by('lon')).
  as('grp').
  project('ladiff','lgdiff','lat1','lon1','lat2','lon2').
    by(project('la1','la2').
         by(select('AUS').select('lat')).
         by(select('DFW').select('lat')).
       math('(la2 - la1) * rdeg')).
    by(project('lg1','lg2').
         by(select('AUS').select('lon')).
         by(select('DFW').select('lon')).
       math('(lg2 - lg1) * rdeg')).
    by(select('grp').select('AUS').select('lat')).
    by(select('grp').select('AUS').select('lon')).
    by(select('grp').select('DFW').select('lat')).
    by(select('grp').select('DFW').select('lon'))
----

Here are the results. A map has been generated containing the differences as well as
the original coordinates.

[source,groovy]
----
[ladiff:0.04716405157034253,
 lgdiff:0.011028683009581777,
 lat1:30.1944999694824,
 lon1:-97.6698989868164,
 lat2:32.896800994873,
 lon2:-97.0380020141602]
----

The purpose of collecting the values like this is so they can be fed into the 'math'
step to complete the calculation. One key takeaway from this example is the way that
a 'math' step can be fed values from a prior 'project' step. The final calculations
perform the necessary trigonometry as required by the Haversine formula. Here is the
complete version of our first attempt at solving this problem in Gremlin.

[source,groovy]
----
g.withSideEffect("rdeg", 0.017453293).
  withSideEffect("gcmiles",3956).
  V().
  has('code',within('AUS','DFW')).
  group().
    by('code').
    by(project('lat','lon').
      by('lat').
      by('lon')).
  as('grp').
  project('ladiff','lgdiff','lat1','lon1','lat2','lon2').
    by(project('la1','la2').
         by(select('AUS').select('lat')).
         by(select('DFW').select('lat')).
       math('(la2 - la1) * rdeg')).
    by(project('lg1','lg2').
         by(select('AUS').select('lon')).
         by(select('DFW').select('lon')).
       math('(lg2 - lg1) * rdeg')).
    by(select('grp').select('AUS').select('lat')).
    by(select('grp').select('AUS').select('lon')).
    by(select('grp').select('DFW').select('lat')).
    by(select('grp').select('DFW').select('lon')).
  math('(sin(ladiff/2))^2 + cos(lat1*rdeg) * cos(lat2*rdeg) * (sin(lgdiff/2))^2').
  math('gcmiles * (2 * asin(sqrt(_)))')
----

We can see that the distance from Austin to Dallas Fort Worth is approximately 190
miles.

[source,groovy]
----
190.2483140396514
----

A few adjustments are needed to make the query more flexible. We wanted to have a
query where you just provide any two airport codes at the start. This makes it easy
to parameterize the query when working with code. The main difference from our first
attempt is that the source and target airports are located at the start and
individually labelled. The `group` step is replaced by a `select` step and given a
label of "grp". The remainder of the query refers to "grp" as needed.

[source,groovy]
----
g.withSideEffect("rdeg", 0.017453293).
  withSideEffect("gcmiles",3956).
  V().has('code','AUS').as('src').
  V().has('code','DFW').as('dst').
  select('src','dst').
    by(project('lat','lon').
         by('lat').
         by('lon')).
  as('grp').
  project('ladiff','lgdiff','lat1','lon1','lat2','lon2').
    by(project('la1','la2').
         by(select('grp').select('src').select('lat')).
         by(select('grp').select('dst').select('lat')).
       math('(la2 - la1) * rdeg')).
    by(project('lg1','lg2').
         by(select('grp').select('src').select('lon')).
         by(select('grp').select('dst').select('lon')).
       math('(lg2 - lg1) * rdeg')).
    by(select('grp').select('src').select('lat')).
    by(select('grp').select('src').select('lon')).
    by(select('grp').select('dst').select('lat')).
    by(select('grp').select('dst').select('lon')).
  math('(sin(ladiff/2))^2 + cos(lat1*rdeg) * cos(lat2*rdeg) * (sin(lgdiff/2))^2').
  math('gcmiles * (2 * asin(sqrt(_)))')
----

The result from the more general purpose version of the query is the same as before.

[source,groovy]
----
190.2483140396514
----

NOTE: The source code in this section comes from the 'great-circle.groovy' sample
located at https://github.com/krlawrence/graph/tree/main/sample-code/groovy.

We can go one step further and parameterize the query. Everything shown below can be
done using the Gremlin Console but it also represents the way you might use this
query within an application. This time San Francisco and Tokyo Narita are used as the
origin and destination.

[source,groovy]
----
start = 'SFO'
stop = 'NRT'

g.withSideEffect("rdeg", 0.017453293).
  withSideEffect("gcmiles",3956).
  V().has('code',start).as('src').
  V().has('code',stop).as('dst').
  select('src','dst').
    by(project('lat','lon').
         by('lat').
         by('lon')).
  as('grp').
  project('ladiff','lgdiff','lat1','lon1','lat2','lon2').
    by(project('la1','la2').
         by(select('grp').select('src').select('lat')).
         by(select('grp').select('dst').select('lat')).
       math('(la2 - la1) * rdeg')).
    by(project('lg1','lg2').
         by(select('grp').select('src').select('lon')).
         by(select('grp').select('dst').select('lon')).
       math('(lg2 - lg1) * rdeg')).
    by(select('grp').select('src').select('lat')).
    by(select('grp').select('src').select('lon')).
    by(select('grp').select('dst').select('lat')).
    by(select('grp').select('dst').select('lon')).
  math('(sin(ladiff/2))^2 + cos(lat1*rdeg) * cos(lat2*rdeg) * (sin(lgdiff/2))^2').
  math('gcmiles * (2 * asin(sqrt(_)))')

5108.80166113392
----

[[via]]
=== Finding routes that go via a specific airport

We have seen a number of examples that use a 'has' step to filter routes. The example
below looks for five routes that start in Austin (AUS) and go via Dallas (DFW).

[source,groovy]
----
g.V().has('code','AUS').
      out().has('code','DFW').
      out().
      limit(5).
      path().by('code')
----

When run the query, as expected finds us five routes that start in AUS and stop at
DFW on the way.

[source,groovy]
----
[AUS,DFW,YYZ]
[AUS,DFW,YVR]
[AUS,DFW,LHR]
[AUS,DFW,CDG]
[AUS,DFW,FRA]
----

This technique works well if we know which stop we want to be in DFW. However, we
might want to write a query that can only return results that include a visit to a
specific airport anywhere in the journey. Let's build an example in two stages. First
of all look at the query below. It looks for ten routes that start in Austin and end
up in Edinburgh only stopping along the way in US or the UK airports.

[source,groovy]
----
g.V().
  has('code','AUS').
  repeat(out().simplePath().has('country',within('US','UK'))).
    until(has('code','EDI')).
  path().by('code').
  limit(10)
----

When run we get the following results.

[source,groovy]
----
[AUS,PHL,EDI]
[AUS,PVD,EDI]
[AUS,BOS,EDI]
[AUS,IAD,EDI]
[AUS,JFK,EDI]
[AUS,MCO,EDI]
[AUS,LHR,EDI]
[AUS,EWR,EDI]
[AUS,LGW,EDI]
[AUS,PHL,SWF,EDI]
----

However, for the sake of making the example more interesting, let's assume that we
only want to get back routes that go via Manchester (MAN). We can do this by
adjusting the prior query to look for "MAN" as it traverses the graph and keep track,
using  a sack, of paths that encounter Manchester along the way. Once we arrive at
EDI we use a 'where' step to filter out routes where the value of the sack is
anything but "1". A value of "1" means we encountered Manchester along the way.

[source,groovy]
----
g.withSack(0).V().
  has('code','AUS').
  repeat(out().simplePath().has('country',within('US','UK')).
         choose(has('code','MAN'),sack(sum).by(constant(1)))).
    until(has('code','EDI')).
  where(sack().is(1)).
  path().by('code').
  limit(10)
----

When the modified query is run, each result includes MAN in the path.

[source,groovy]
----
[AUS,PHL,MAN,EDI]
[AUS,ATL,MAN,EDI]
[AUS,BOS,MAN,EDI]
[AUS,IAD,MAN,EDI]
[AUS,IAH,MAN,EDI]
[AUS,JFK,MAN,EDI]
[AUS,LAX,MAN,EDI]
[AUS,MCO,MAN,EDI]
[AUS,MIA,MAN,EDI]
[AUS,ORD,MAN,EDI]
----

If we you increase the limit step to use a value such as 30, then you will start to
see that MAN can appear in any position in the path.

[source,groovy]
----
[AUS,LHR,GLA,MAN,EDI]
[AUS,LHR,ABZ,MAN,EDI]
[AUS,LHR,BHD,MAN,EDI]
[AUS,LHR,INV,MAN,EDI]
----

[[storesideeffect]]
=== Using 'store' and a 'sideEffect' to make a set of unique values

Take a look at the query below. All it does is return the city names for the airports
that have IDs between 1 and 200 (inclusive). The list is sorted by ascending 
alphabetical order.

[source,groovy]
----
g.V().hasId(between(1,201)).values('city').order().fold()
----

And here are the names that get returned. If you look closely at the list you
will see that city names like 'Dallas' and 'London' appear more than once.

[source,text]
----
[Abu Dhabi,Addis Ababa,Albuquerque,Alicante,Alice Springs,Amsterdam,Anchorage,Athens,
 Atlanta,Auckland,Austin,Ayers Rock,Baltimore,Bangkok,Barcelona,Beijing,Belgrade,
 Bengaluru,Berlin,Bilbao,Bologna,Boston,Brisbane,Brussels,Budapest,Buenos Aires,
 Cairns,Cairo,Calgary,Calicut,Canberra,Cancun,Cape Town,Cedar Rapids,Charlotte,
 Chennai,Chicago,Chicago,Christchurch,Cincinnati,Cleveland,Cologne,Copenhagen,
 Dallas,Dallas,Denver,Detroit,Doha,Dubai,Dublin,Durban,Dusseldorf,Edinburgh,
 Edmonton,El Paso,Fairbanks,Fort Lauderdale,Fort Myers,Frankfurt,Geneva,Genoa,
 Glasgow,Gold Coast,Gothenburg,Hagta,Halifax,Hamburg,Harrison,Helsinki,
 Ho Chi Minh City,Hong Kong,Honolulu,Houston,Houston,Hyderabad,Ibiza,
 Indianapolis,Istanbul,Johannesburg,Kahului,Kansas City,Kingston,Kolkata,
 Kuala Lumpur,Kuwait,Larnaca,Las Vegas,Lima,Liverpool,London,London,London,London,
 Long Beach,Los Angeles,Luqa,Luxembourg,Madrid,Manama,Manchester,Manila,Maroochydore,
 Melbourne,Memphis,Menorca,Mexico City,Miami,Milan,Milwaukee,Minneapolis,Mombasa,
 Montevideo,Montreal,Moscow,Moscow,Mumbai,Munich,Nairobi,Nantes,Naples,Nashville,
 New Delhi,New Orleans,New York,New York,Newark,Nice,Nottingham,Oaklahoma City,
 Oakland,Omaha,Ontario,Orlando,Osaka,Oslo,Ottawa,Palm Springs,Paris,Paris,Perth,
 Philadelphia,Phnom Penh,Phoenix,Pisa,Pittsburgh,Portland,Portland,Prague,
 Puerto Vallarta,Raleigh,Rio de Janeiro,Rochester,Rochester,Rome,Salina,
 Salt Lake City,San Antonio,San Diego,San Francisco,San Jose,San Juan,Santa Ana,
 Santa Fe,Santiago,Sao Paulo,Seattle,Seoul,Shanghai,Shannon,Singapore,Sofia,St Louis,
 St. John's,Stockholm,Stuttgart,Sydney,Tallahassee,Tampa,Tel Aviv,Tenerife,Tokyo,
 Tokyo,Toronto,Tucson,Tulsa,Turin,Vancouver,Venice,Venice,Verona,Vienna,Warsaw,
 Washington D.C.,Washington D.C.,Wellington,West Palm Beach,White Plains,Winnipeg,
 Zagreb,Zurich]
----

Just to be sure, we can count how many city names we got back.

[source,groovy]
----
g.V().hasId(between(1,201)).values('city').order().count()

200
----

What would be nice is if the duplicate names only appeared once. There are other ways
we could write this query such as using 'dedup' but let's rewrite it using 'store'
and a 'Set' as we think doing that demonstrates a capability quite well that is
useful in more complex scenarios than this one. By starting a query using
'withSideEffect' we can setup a named place we can 'store' things into later in our
query and we can also give the store a type. In this case we chose to use a 'Set'.
What this query does is store the City names of the first 200 vertices in the graph 
into a Set and then displays them. As we know from our first attempt, city names, 
like Dallas, appear more than once. However if we look at the Set that we get back 
from our modified query (because by default Sets do not store duplicates) we will 
only see the names Dallas and London appearing once.

[source,groovy]
----
g.withSideEffect("x", [] as Set).V().hasId(between(1,201)).
  values('city').aggregate('x').cap('x').unfold().order().fold()
----

Here are the city names that we get back after running our modified query. You will 
notice that all of the duplicate names are now gone.

[source,text]
----
[Abu Dhabi,Addis Ababa,Albuquerque,Alicante,Alice Springs,Amsterdam,Anchorage,
 Athens,Atlanta,Auckland,Austin,Ayers Rock,Baltimore,Bangkok,Barcelona,Beijing,
 Belgrade,Bengaluru,Berlin,Bilbao,Bologna,Boston,Brisbane,Brussels,Budapest,
 Buenos Aires,Cairns,Cairo,Calgary,Calicut,Canberra,Cancun,Cape Town,Cedar Rapids,
 Charlotte,Chennai,Chicago,Christchurch,Cincinnati,Cleveland,Cologne,Copenhagen,
 Dallas,Denver,Detroit,Doha,Dubai,Dublin,Durban,Dusseldorf,Edinburgh,Edmonton,
 El Paso,Fairbanks,Fort Lauderdale,Fort Myers,Frankfurt,Geneva,Genoa,Glasgow,
 Gold Coast,Gothenburg,Hagta,Halifax,Hamburg,Harrison,Helsinki,Ho Chi Minh City,
 Hong Kong,Honolulu,Houston,Hyderabad,Ibiza,Indianapolis,Istanbul,Johannesburg,
 Kahului,Kansas City,Kingston,Kolkata,Kuala Lumpur,Kuwait,Larnaca,Las Vegas,Lima,
 Liverpool,London,Long Beach,Los Angeles,Luqa,Luxembourg,Madrid,Manama,Manchester,
 Manila,Maroochydore,Melbourne,Memphis,Menorca,Mexico City,Miami,Milan,Milwaukee,
 Minneapolis,Mombasa,Montevideo,Montreal,Moscow,Mumbai,Munich,Nairobi,Nantes,Naples,
 Nashville,New Delhi,New Orleans,New York,Newark,Nice,Nottingham,Oaklahoma City,
 Oakland,Omaha,Ontario,Orlando,Osaka,Oslo,Ottawa,Palm Springs,Paris,Perth,
 Philadelphia,Phnom Penh,Phoenix,Pisa,Pittsburgh,Portland,Prague,Puerto Vallarta,
 Raleigh,Rio de Janeiro,Rochester,Rome,Salina,Salt Lake City,San Antonio,San Diego,
 San Francisco,San Jose,San Juan,Santa Ana,Santa Fe,Santiago,Sao Paulo,Seattle,
 Seoul,Shanghai,Shannon,Singapore,Sofia,St Louis,St. John's,Stockholm,Stuttgart,
 Sydney,Tallahassee,Tampa,Tel Aviv,Tenerife,Tokyo,Toronto,Tucson,Tulsa,Turin,
 Vancouver,Venice,Verona,Vienna,Warsaw,Washington D.C.,Wellington,West Palm Beach,
 White Plains,Winnipeg,Zagreb,Zurich]
----

And just to make sure we got fewer names back this time let's count them again.

[source,groovy]
----
g.withSideEffect("x", [] as Set).V().hasId(between(1,201)).
  values('city').aggregate('x').cap('x').unfold().count()

186
----

As we mentioned at the start of this section, you could achieve this result other
ways. For example, here is a version of the query that uses 'dedup' instead of
'store'.

[source,groovy]
----
g.V().hasId(between(1,201)).values('city').dedup().count()

186
----

This is clearly a simpler query in this case but you will find cases where the 
example that uses 'withSideEffect' and 'store' will come in very handy, especially in
cases where you want to store things into a Set or List from multiple parts of a 
traversal such as the one below that finds and counts all the unique city names 
across multiple hops from a starting airport.

[source,groovy]
----
g.withSideEffect("x", [] as Set).V().hasId(3).as('a').values('city').aggregate('x').
   select('a').out().as('b').values('city').aggregate('x').
   select('b').out().values('city').aggregate('x').cap('x').unfold().count()

977
----

Lastly on this topic, let's look at one more interesting use case. It is quite common
to want to get back from a query a collection of vertices and edges. This is often
because we want to examine properties on both the vertices and the edges. Imagine a
small graph that has the following relationships.

(A->B),
(A->C),
(A->D),
(C->D),
(C->E),
(D->F)

The code below can be used to create this graph using the Gremlin console and
TinkerGraph.

[source,groovy]
----
graph = TinkerGraph.open()
g = traversal().with(graph)

g.addV("A").as("a").
  addV("B").as("b").
  addV("C").as("c").
  addV("D").as("d").
  addV("E").as("e").
  addV("F").as("f").
  addE("knows").from("a").to("b").
  addE("knows").from("a").to("c").
  addE("knows").from("a").to("d").
  addE("knows").from("c").to("d").
  addE("knows").from("c").to("e").
  addE("knows").from("d").to("f")
----

We can see the IDs that were allocated for each vertex by looking at the 'valueMap'.

[source,groovy]
----
g.V().valueMap(true)

[id:0,label:A]
[id:1,label:B]
[id:2,label:C]
[id:3,label:D]
[id:4,label:E]
[id:5,label:F]
----

Likewise we can look at the edges to see what IDs each edge was given.

[source,groovy]
----
g.E()

e[6][0-knows->1]
e[7][0-knows->2]
e[8][0-knows->3]
e[9][2-knows->3]
e[10][2-knows->4]
e[11][3-knows->5]
----

Now that we have our test graph created let's take a look at the query we will need
to develop. The problem we want to solve is to start from vertex A, find all of the
edges that go out from vertex A and also all of the vertices at the other ends of 
those edges. Lastly we also want to find any edges between the vertices that are also
connected to A but ignore edges that connect to vertices that are not also connected 
to A. In simple terms we want a query that will return all of the relationships 
except (C->E) and (D-F) as A is not connected to E or F.

Using the 'withSideEffect' pattern that we used earlier in this section we can again
develop a query that will collect for us the vertices and edges that we are 
interested in. We added line numbers to make it easier to discuss what is going on 
but please, note that these are not part of the query itself.

[source,groovy]
----
1: g.withSideEffect('x', [] as Set).
2:     V(0L).aggregate('x').
3:     bothE().local(aggregate('x')).
4:     otherV().local(aggregate('x')).
5:     aggregate('tgtlist').
6:     bothE().as('ref').otherV().where(within('tgtlist')).
7:     select('ref').aggregate('x').cap('x').unfold()
----

Let's look at the query above line by line.

1. Start the query and define 'x' as our, initially empty, Set.
2. Start at vertex 0 and 'aggregate' it into our set 'x'.
3. Aggregate all of the edges connected to 'V(0)' into our set. We use 'local' here
to force an eager evaluation of the stream.
4. Aggregate the vertices connected to 'V(0)' into our set.
5. Aggregate all of these target vertices into 'tgtlist'.
6. Find more edges but only remember them if they connect to vertices also connected
to 'V(0)'.
7. Aggregate the edges we found in 'x' and finally return the set as the overall 
result   the query. The 'unfold' just makes the output a little easier to read.

Here is the output we get from running the query. As you can see, the vertices and 
edges that we were not interested in have been correctly left out of the result set.

[source,groovy]
----
v[0]
e[5][0-knows->1]
v[1]
e[6][0-knows->2]
v[2]
e[7][0-knows->3]
v[3]
e[8][2-knows->3]
----

As you start to work with graphs and start to do more complex querying, this pattern
of query based around 'withSideEffect' is extremely useful to keep in mind.

[[dfwcopy]]
=== Making a copy of the DFW vertex

It is sometimes useful to be able to create a new vertex using the label and
properties from one or more existing vertices. In this section We are going to
present a small case study that shows how we investigated the creation of a query
that could create a copy of the Dallas Fort Worth (DFW) airport vertex.

Creating a vertex using the label from another vertex is quite straightforward. The
two queries shown below use the label from the DFW Airport vertex as the label for a
new vertex.

Our first thought was to do this as follows.

[source,groovy]
----
g.addV().property(label,V().has('code','DFW').label())
----

The query above does work but it felt a bit cumbersome and we also realized we were
going to need a way to refer to the DFW vertex more than once so we changed the query
as follows and ran it.

[source,groovy]
----
g.V().has('code','DFW').as('dfw').
      addV().property(label, select('dfw').label())

v[61395]
----

If we examine the new vertex we can see that it does indeed have the correct label.

[source,groovy]
----
g.V(61395).valueMap(true)

[label:airport,id:61395]
----

Now we need to expand the query to also copy the property key names and their values
from the DFW vertex into our new vertex. Earlier in the book we looked at ways that a
'select' step combined with the 'keys' and 'values' keywords can be used to select
values from a map. We can use that same technique here.

First of all let's remind ourselves about the properties of the DFW airport.

[source,groovy]
----
g.V().has('code','DFW').properties()

vp[country->US]
vp[code->DFW]
vp[longest->13401]
vp[city->Dallas]
vp[elev->607]
vp[icao->KDFW]
vp[lon->-97.0380020141602]
vp[type->airport]
vp[region->US-TX]
vp[runways->7]
vp[lat->32.896800994873]
vp[desc->Dallas/Fort Worth...]
----

Let's also count how many properties there are listed above.

[source,groovy]
----
g.V().has('code','DFW').properties().count()

12
----

So we need to add some steps to our query that will copy the twelve keys and values
from these twelve properties belonging to the DFW vertex into our new one.

Below is the first query we tried when thinking about how to do this. The goal of the
query is to first capture the properties from the DFW airport and then to add them to
the new vertex being created.

[source,groovy]
----
g.V().has('code','DFW').as('dfw').
      addV().property(label, select('dfw').label()).as('new').
             property(select('dfw').properties().key(),
                      select('dfw').properties().value())
----

When we ran it we got this result which looked encouraging as a new vertex had
clearly been created.

[source,groovy]
----
v[61396]
----

However, when we inspected our new vertex we saw we had only managed to copy one of
the twelve properties over. So it was time to delete that vertex and have a bit of a
rethink.

[source,groovy]
----
g.V(61396).valueMap(true).unfold()

country=[US]
label=airport
id=61396

g.V(61396).drop()
----

What we realized was that our first attempt at copying the properties was only ever
going to copy one property as that was essentially what we had asked it to do. If
it's not quite clear why this is the case, hopefully it will become clearer once you
look at the results from the profile step that are shown later in this section. We
realized that what we needed to do was to first get all of the DFW properties and
then add them to the new vertex. So we changed the query as shown below.

[source,groovy]
----
g.V().has('code','DFW').as('dfw').
      addV().property(label, select('dfw').label()).as('new').
      select('dfw').properties().as('dfwprops').
      select('new').property(select('dfwprops').key(),
                             select('dfwprops').value())
----

When run, this is what we saw in the Gremlin console. This is more output than we
wanted to get from the query but it has clearly done more work this time. If you
count the number of rows below you will find it also adds up to twelve. So this time
we got one result per property it would appear.

[source,groovy]
----
v[61398]
v[61398]
v[61398]
v[61398]
v[61398]
v[61398]
v[61398]
v[61398]
v[61398]
v[61398]
v[61398]
v[61398]
----

In a moment we will explore a way to get rid of the unwanted output but first let's
examine the contents of the new vertex.

[source,groovy]
----
g.V(61398).valueMap(true).unfold()

country=[US]
code=[DFW]
longest=[13401]
city=[Dallas]
lon=[-97.0380020141602]
type=[airport]
elev=[607]
label=airport
icao=[KDFW]
id=61398
region=[US-TX]
runways=[7]
lat=[32.896800994873]
desc=[Dallas/Fort Worth International Airport]
----

So a large part of our original goal has now been achieved. A vertex has been created
that is a copy in all but ID value of the original DFW vertex. So, what can be done
about the unwanted list of vertices that came back as the results from the query?

First of all, let's take a look at what the 'profile' step can tell us about this
query. Note that we deleted the new vertex using a 'drop' step before running this
query so it did not pick up both the old and new DFW vertices.

[source,groovy]
----
g.V(61398).drop()

g.V().has('code','DFW').as('dfw').
      addV().property(label, select('dfw').label()).as('new').
      select('dfw').properties().as('dfwprops').
      select('new').property(select('dfwprops').key(),
                             select('dfwprops').value()).profile()

----

If you look at the results below you can see that for each property key/value pair,
a traverser was spawned. This explains why we got twelve results back. This also
explains why our first attempt at writing this query failed. If you were to profile
that query you would find that it only spawns one traverser rather than the twelve
that we need to be successful.


[source,console]
----
Traversal Metrics      Step                      Count Traver-  Time   %Dur
                                                       sers      (ms)
===================================================================================
TinkerGraphStep(vertex,[code.eq(DFW)])@[dfw]        1     1    1.416    28.85
AddVertexStep({label=[[SelectOneStep(last,dfw),...  1     1    1.840    37.47
  SelectOneStep(last,dfw)                           1     1    0.171
  NoOpBarrierStep(2500)                             1     1    0.092
  LabelStep                                         1     1    0.059
SelectOneStep(last,dfw)                             1     1    0.132     2.70
NoOpBarrierStep(2500)                               1     1    0.032     0.66
PropertiesStep(property)@[dfwprops]                12    12    0.094     1.92
SelectOneStep(last,new)                            12    12    0.127     2.60
NoOpBarrierStep(2500)                              12    12    0.065     1.34
AddPropertyStep({value=[[SelectOneStep(last,dfw... 12    12    1.200    24.45
  SelectOneStep(last,dfwprops)                     12    12    0.168
  NoOpBarrierStep(2500)                            12    12    0.064
  PropertyKeyStep                                  12    12    0.066
  SelectOneStep(last,dfwprops)                     12    12    0.134
  NoOpBarrierStep(2500)                            12    12    0.070
  PropertyValueStep                                12    12    0.043
                                            >TOTAL  -     -    4.911

----

As you will hopefully recall from our discussion of the 'addV' and 'property' steps
earlier in Chapter 4, a 'property' step returns the vertex that the property was
added to and not the new property itself. This allows multiple 'addV' and 'property'
steps to be chained together.

So we need a way to still create our new properties but not have the twelve
traversers return any results to us. Hopefully your reaction to this is something
like "This feels like a good place to introduce a 'sideEffect' step into the query".
If that was your reaction you are right!

Before running the new query we need to clean up the work done by the prior one.

[source,groovy]
----
g.V().has('code','DFW')

v[8]
v[61411]

g.V(61411).drop()
----

So now let's wrap the creation of the properties into a 'sideEffect' step that should
stop the unwanted results from being returned.The modified  version of the query is
shown below.

[source,groovy]
----
g.V().has('code','DFW').as('dfw').
      addV().property(label, select('dfw').label()).as('new').
      sideEffect(select('dfw').properties().as('dfwprops').
                 select('new').property(select('dfwprops').key(),
                                        select('dfwprops').value()))
----

When we run the modified version of the query this is what we get back.

[source,groovy]
----
[61424]
----

This is a lot better as we now just got back the new vertex one time as the result.
Let's double check that the query has worked as intended.

[source,groovy]
----
g.V(61424).valueMap(true).unfold()

country=[US]
code=[DFW]
longest=[13401]
city=[Dallas]
lon=[-97.0380020141602]
type=[airport]
elev=[607]
label=airport
icao=[KDFW]
id=61424
region=[US-TX]
runways=[7]
lat=[32.896800994873]
desc=[Dallas/Fort Worth International Airport]
----

In this small case study we have tried to show how we were able to evolve a query in
stages and also to adapt to unexpected outcomes along the way. You could use
essentially the same technique shown in this section to also copy one or more edges
if you needed to.

[[btree]]
=== Modelling an ordered binary tree as a graph

You can of course model a tree structure as a graph. The following code will create a
new graph containing an ordered binary tree. A graph like this is sometimes referred
to as a 'connected acyclic' graph as there are no cycles in the graph. This means
that once you leave a node there is no other path you could take that will allow you
to get there again. By contrast the air routes graph is an example of a 'cyclic'
graph as there are clearly many ways to revisit vertices.

[source,groovy]
----
// Builds a small ordered Binary (BST) Tree
graph = TinkerGraph.open()
g = traversal().with(graph)
g.addV('root').property('data',9).as('root').
  addV('node').property('data',5).as('b').
  addV('node').property('data',2).as('c').
  addV('node').property('data',11).as('d').
  addV('node').property('data',15).as('e').
  addV('node').property('data',10).as('f').
  addV('node').property('data',1).as('g').
  addV('node').property('data',8).as('h').
  addV('node').property('data',22).as('i').
  addV('node').property('data',16).as('j').
  addE('left').from('root').to('b').
  addE('left').from('b').to('c').
  addE('right').from('root').to('d').
  addE('right').from('d').to('e').
  addE('right').from('e').to('i').
  addE('left').from('i').to('j').
  addE('left').from('d').to('f').
  addE('right').from('b').to('h').
  addE('left').from('c').to('g')
----

We could of course use the 'max' and the 'min' steps to find the largest and smallest
values in the graph. However, the queries below show how we can do it using the
semantics of an ordered binary tree.

[source,groovy]
----
// Find the largest value in the graph
g.V().hasLabel('root').repeat(out('right')).
                       until(out('right').count().is(0)).values('data')

22

// Find the smallest value in the graph
g.V().hasLabel('root').repeat(out('left')).
                       until(out('left').count().is(0)).values('data')

1
----

As a side note, here is a different way we could have written the query using 'not'
instead of 'count'. As 'not' is a reserved word in Groovy, as we discussed
in the "<<rword>>" section, we have to prefix it with the '__.' notation.

[source,groovy]
----
g.V().hasLabel('root').repeat(out('left')).
                       until(__.not(out('left'))).values('data')
----

If we wanted to see the values that the 'repeat' is encountering as it traverses the
tree we could add an 'emit' step and get the values of each node the 'repeat' visits.
Note that this does not include the value from the root node.

[source,groovy]
----
g.V().hasLabel('root').repeat(out('left')).emit().values('data')

5
2
1
----

Perhaps a nicer way to look at all the values we encountered as we traversed the tree
would be to use 'path' as follows. Note that this does include the root node's value.

[source,groovy]
----
g.V().hasLabel('root').repeat(out('left')).
                       until(__.not(out('left'))).path().by('data')

[9,5,2,1]
----

We can see all of the possible paths through the tree by running the following query.

[source,groovy]
----
g.V().hasLabel('root').repeat(out()).times(4).emit().path().by('data')

[9,5]
[9,11]
[9,5,2]
[9,5,8]
[9,11,10]
[9,11,15]
[9,5,2,1]
[9,11,15,22]
[9,11,15,22,16]
----

We briefly explored the TinkerPop Tree API in the "<<tre>>" section. We could use
what we discussed there to create a Tree object from our Binary Tree graph as
follows.


[source,groovy]
----
t=g.V().hasLabel('root').repeat(out()).emit().tree().by('data').next()
----

Just to be sure we can query what kind of object we just created.

[source,groovy]
----
t.getClass()

class org.apache.tinkerpop.gremlin.process.traversal.step.util.Tree
----

If we print the tree we can see how it has been created from our original graph. If
you study the nesting closely you will see that it does indeed represent the original
binary tree data that we used to create the graph.

[source,groovy]
----
println t

[9:[5:[2:[1:[:]], 8:[:]], 11:[10:[:], 15:[22:[16:[:]]]]]]
----

We can use the 'getObjectsAtDepth' method to further investigate the tree structure.

[source,groovy]
----
t.getObjectsAtDepth(1)

9

t.getObjectsAtDepth(2)

5
11

t.getObjectsAtDepth(3)

2
8
10
15

t.getObjectsAtDepth(4)

1
22

t.getObjectsAtDepth(5)

16
----

[[randwalk]]
=== Randomly walking a graph

When doing analysis of a graph sometimes you just want to randomly traverse or 'walk'
parts of the graph. The example below shows a query that starts at the Austin (AUS)
vertex and then randomly goes to five connected vertices from there. The 'random
walk' is achieved by picking a sample of one of the possible edges connected to the
vertex we are currently at by sampling using the distance property of the edge and
then moving to the vertex at the other end of that edge.

[source,groovy]
----
// Random walk with five hops
g.V().has('code','AUS').
      repeat(bothE('route').
         sample(1).by('dist').otherV()).
      times(5).
      path().by('code').by('dist')
----

Below are the results of running the query five times. You can see each graph walk
starts at AUS and then goes to five places from there. The path shown displays the
names of the other airports and the distances between them.

[source,groovy]
----
[AUS,4921,LGW,2338,AQJ,1915,BER,992,SVO,6059,LAX]

[AUS,5294,FRA,1859,ACE,2078,HAM,1365,DJE,1001,MUC]

[AUS,1869,YVR,5698,HGH,4156,DOH,8030,IAH,2067,STT]

[AUS,5074,AMS,4507,SFB,1335,AUA,2019,IAH,304,MSY]

[AUS,5074,AMS,4905,DFW,1056,CZM,1653,YYZ,7725,CAN]
----

In the previous example, every random walk began at the same place. However, if we
wanted a more random walk that starts from a different airport each time we could
instead use a 'sample' step to at the start of the query to pick a random airport to
start from out of the set of all vertices with an 'airport' label.

[source,groovy]
----
// Random walk with five hops
g.V().hasLabel('airport').sample(1).
      repeat(bothE('route').
        sample(1).by('dist').otherV()).
      times(5).
      path().by('code').by('dist')
----

When run five times, as shown below, each walk begins at a different airport.

[source,groovy]
----
[CRP,355,DFW,5119,GRU,1675,MAO,1768,GIG,5746,LHR]

[ACA,190,MEX,2333,SEA,4807,LGW,4921,AUS,1072,CMH]

[YFC,443,YOW,751,CLT,4564,MUC,862,VLC,2064,DME]

[AUQ,891,PPT,4200,SFO,7688,DEL,6480,SYD,723,ADL]

[RMA,273,BNE,3999,KUL,6810,HNL,3755,AUS,919,IND]
----

Another way to write the query involves the introduction of a 'local' step. In this
case the 'repeat' step is applied against the current local state of the traversal.
In this way we can achieve multiple random walks. The 'limit(5)' at the end of the
query limits the number of random walks returned to just five. Note that if the
'local' step was removed only one walk would occur.

[source,groovy]
----
// Five random walks each of five hops
g.V().hasLabel('airport').
      repeat(local(bothE('route').
        sample(1).by('dist').otherV())).
      times(5).
      path().by('code').by('dist').limit(5)
----

When run, five results such as those shown below are returned.

[source,groovy]
----
[ATL,2364,UIO,1619,HAV,6183,IST,1483,JED,1421,TBS]
[ANC,763,SNP,763,ANC,722,BRW,151,NUI,151,BRW]
[AUS,919,IND,816,BOS,1610,HOU,998,SJD,1967,YWG]
[BNA,490,MCI,1343,CUN,1437,CLE,692,MCI,1045,PIE]
[BOS,2580,SAN,2253,IAD,960,NAS,4332,LHR,250,IOM]
----

[[sevendegrees]]
=== Seven degrees of separation!

No, the word seven in the heading above is not a mistake, read on! Most people we are
sure have heard of the famous "Six degrees of separation" theory. The theory
essentially states that for any living person, using "friend of a friend" style
connections, none of us is farther removed than six friendship relationships from
anyone else.

NOTE: You can read more about the Six degrees of separation theory on Wikipedia at
this location https://en.wikipedia.org/wiki/Six_degrees_of_separation.

We decided it would be fun to run an experiment using the `air-routes` data set to
see how far any airport, that is not a known orphan vertex, is from a major hub
airport. For this experiment we decided to use London Heathrow (LHR) airport as our
target.

We came up with the queries below in order to do this. The first thing we wanted to
do was establish how many connections we would be looking for.

First of all we double checked how many airports are in the graph.

[source,groovy]
----
g.V().hasLabel('airport').count()

3504
----

Next we remembered there are some orphan airports that have no routes so we have to
rule those out. We can do this using a technique similar to the one that was
discussed in the "<<noroutes>>" section.

[source,groovy]
----
g.V().hasLabel('airport').not(outE('route')).count()

29
----

So we know that there are 16 airports we need to ignore for this experiment. Leaving
us with 3475 airports we care about but one of those will be LHR so we also need to
discount that one as we are not looking for cyclic paths. So, we need a query that
proves there is a route between all of these 3475 remaining airports and London
Heathrow but ignores the orphan airports and Heathrow itself as starting points.

The core part of the query is shown below. We started out using just one hop for
testing purposes. The query starts by ignoring LHR and the orphan nodes. It next uses
a 'union' step within 'local' scope to try and find a single path in one hop between
the starting airport and LHR. The result, for each starting airport will either be a
path containing the codes of all the airports from the starting airport to LHR or if
LHR was not reached the path will just contain the starting airport. Just to test
this part of the query we used a hop count of 1 and limited the results to just 10.

[source,groovy]
----
g.V().has('airport','code',neq('LHR')).
       filter(out('route').count().is(neq(0))).
       local(union(
              identity().values('code'),
              local(repeat(out().simplePath()).
                      emit().times(1).
                      has('code','LHR').
                      limit(1)).
              path().by('code')).
              fold()).
        limit(10)
----

As you can see, the query we have so far seems to be working. We either get back a
starting airport and a path or just a starting airport if no route was found. This is
why we used the 'union' step to guarantee that at a minimum the starting airport is
returned. The 'local' scope is used as we wanted the results for each starting
airport to be in a separate list.

[source,groovy]
----
[ATL,[ATL,LHR]]
[ANC]
[AUS,[AUS,LHR]]
[BNA,[BNA,LHR]]
[BOS,[BOS,LHR]]
[BWI,[BWI,LHR]]
[DCA]
[DFW,[DFW,LHR]]
[FLL]
[IAD,[IAD,LHR]]
----

The final piece missing from the query is a way to decide if we reached LHR and count
the number of times we succeeded in doing so. This is done by checking to see that
the length of the result returned has more than one item in it. In other words, did
we get back more than just the starting airport code. To do this, the 'where' step
filters out the paths that did not reach LHR in the specified number of steps.
Finally, rather than displaying the paths as we did in our testing, we now count how
many of the starting airports were able to get to LHR. For our first round of tests
we decided to see how many of the airports can reach LHR in five hops.

[source,groovy]
----
g.V().has('airport','code',neq('LHR')).
      filter(out('route').count().is(neq(0))).
      local(union(
             identity().values('code'),
             local(repeat(out().simplePath()).
                     emit().times(5).
                     has('code','LHR').
                     limit(1)).
             path().by('code')).
             fold().
             where(count(local).is(gt(1)))).
      count()

3452
----

As you can see, with five hops were were able to reach LHR from 3345 of the 3357
airports we are testing. Next we ran the query again but using six hops.

[source,groovy]
----
g.V().has('airport','code',neq('LHR')).
      filter(out('route').count().is(neq(0))).
      local(union(
             identity().values('code'),
             local(repeat(out().simplePath()).
                     emit().times(6).
                     has('code','LHR').
                     limit(1)).
             path().by('code')).
             fold().
             where(count(local).is(gt(1)))).
      count()

3461
----

So with six hops 3354 of our 3357 airports were able to get to LHR. We then tried
seven hops and found that all but two of the airports were able to reach LHR

[source,groovy]
----
g.V().has('airport','code',neq('LHR')).
      filter(out('route').count().is(neq(0))).
      local(union(
             identity().values('code'),
             local(repeat(out().simplePath()).
                     emit().times(7).
                     has('code','LHR').
                     limit(1)).
             path().by('code')).
             fold().
             where(count(local).is(gt(1)))).
      count()

3462
----

This made me curious. We were fairly convinced that there are no airports in the
graph that would not be able to reach LHR in seven hops unless of course they were
orphans but we have already ruled those out as part of our query. So, we decided to
go with 30 hops just to make sure. However we still got the answer 3355.

[source,groovy]
----
g.V().has('airport','code',neq('LHR')).
      filter(out('route').count().is(neq(0))).
      local(union(
             identity().values('code'),
             local(repeat(out().simplePath()).
                     emit().times(30).
                     has('code','LHR').
                     limit(1)).
             path().by('code')).
             fold().
             where(count(local).is(gt(1)))).
      count()

3462
----

This left me with an interesting conclusion. There must be two airports in the graph
that are not orphans, in other words they have some routes, but they are isolated
from the main network. So we needed a query to find them which is the subject of the
next section. What the queries in this section did prove is that from any airport in
the graph, with the exception of the two cases we need to investigate further, London
LHR can be reached in seven hops or less. Hence the title of this section being
"Seven degrees of separation" rather than "Six" !

[[mininetworks]]
==== Finding isolated sub-networks

As discussed above, while we were working on the queries for the previous section we
realized that there are two airports in the graph that are not true orphans, as they
do have routes between each other but that they are part of a small network that is
not itself connected to the main route network. In other words, the network is an
isolated mini network within the main graph and you can never get to London from
either one of them. After more analysis we realized that this was actually due to an
error in our data set which we have fixed in subsequent updates but if you are using
the version 0.77 level of `air-routes` then the following query will yield the
results shown.

NOTE: The 0.77 dataset was used in the first edition of this book. It can be found in
https://github.com/krlawrence/graph/blob/main/sample-data/air-routes-edition-1.graphml
as a reference.

To solve the puzzle of the missing airports, we only had to slightly modify the seven
degrees of separation query. Note that if you use the most recent version of the
`air-routes` data set, this particular error is fixed, but you will find other
isolated networks within the main graph exist. However, these are not due to errors
but rather because there truly are a set of airports in Portugal connected to each
other via commuter airlines but with no way to get from any one of them to the main,
worldwide route network.

So, here is the modified form of the query.

[source,groovy]
----
g.V().has('airport','code',neq('LHR')).
      filter(out('route').count().is(neq(0))).
      local(union(
             identity().values('code'),
             local(repeat(out().simplePath()).
                     emit().times(7).
                     has('code','LHR').
                     limit(1)).
             path().by('code')).
             fold().
             where(count(local).is(1))).
       unfold()
----

The only change made to this query from the one used in the prior section is that the
'where' step has been changed to 'where(count(local).is(1)))' so that it now looks
for paths of length one. These represent the airports for which no route to LHR was
found. Then rather than 'count' the paths an 'unfold' is used to return the codes for
the airports that had no route.

When the query is run, the codes for the airports with no route to London are
revealed. These airports are truly connected to each other but isolated from the main
route network.

[source,groovy]
----
HPB
VAK
----

Just to verify our findings we ran a couple of queries to double check on the routes
from each of these airports. As you can see they were just connected to each other.

[source,groovy]
----
g.V().has('code','HPB').out().path().by('code')
[HPB,VAK]

g.V().has('code','VAK').out().path().by('code')
[VAK,HPB]
----

This proved both an interesting and useful exercise and uncovered some errors in the
graph that had slipped through our error checking!

[[longestroutes]]
=== Looking for the journey requiring the most stops

Sometimes it is interesting to ponder questions such as "Starting from a given
airport what are the most difficult places to get to?". For the sake of this example
let's define "difficult to get to" as meaning "requires the most hops". By modifying
the query from the previous section we can come up with a way to detect some longest
routes. At the end of the section we have also included examples that show
alternative ways to generate the same result.

The query shown below finds every airport that has out going routes and is not Austin
(AUS). For each airport found an attempt is made to get to Austin in ten or fewer
hops. For each airport the attempt is only made once. We decided to use a value of 10
as we were already fairly confident that there would be no airports further away in
terms of stops than that. However, it really does not matter what value is used so
long as it is big enough. A value of 100 does not adversely affect the query given
that the 'emit' step will return a result as soon as AUS is reached. The query will
find a single path between each starting airport and AUS. The 'where' step on the
last line of the query only keeps any paths that have a length of eight or more. So
essentially that says find me only routes that take at least seven hops to get to
Austin as AUS.

[source,groovy]
----
g.V().has('airport','code',neq('AUS')).
      filter(out('route').count().is(neq(0))).
      local(repeat(out().simplePath()).
              emit(has('code','AUS')).
              times(10).limit(1)).
      path().by('code').
      where(count(local).is(gte(8)))
----

When run, the query may take a few seconds to complete, but should not take more than
that on a typical laptop or desktop. Here are the results of running the query.

[source,groovy]
----
[YPO,YAT,ZKE,YFA,YMO,YTS,YYZ,AUS]
[YZG,YIK,AKV,YPX,YGL,YUL,PHL,AUS]
[THU,NAQ,JUV,JAV,GOH,KEF,PIT,AUS]
----

The query could also be written using 'repeat' and 'until' steps as shown below.

[source,groovy]
----
g.V().has('airport','code',neq('AUS')).
      filter(out('route').count().is(neq(0))).
      local(repeat(out().simplePath()).
        until(has('code','AUS').or().loops().is(8)).
        has('code','AUS').limit(1)).
      path().by('code').
      where(count(local).is(gte(8)))
----

As before, the same three paths are found.

[source,groovy]
----
[YPO,YAT,ZKE,YFA,YMO,YTS,YYZ,AUS]
[YZG,YIK,AKV,YPX,YGL,YUL,PHL,AUS]
[THU,NAQ,JUV,JAV,GOH,KEF,PIT,AUS]
----

[[quicklyfindhardest]]
==== Quickly finding the hardest to get to airports

There are other ways we could write a query that looks for "hard to get to" airports.
The technique shown below essentially relies on using a de-duplication strategy to
make sure you only visit an airport exactly once. Note this is different from the way
the 'sideEffect' step works. That step allows a query to fan out and visit the same
vertex from many places, a many to one type of pattern, but will not allow a
traversal to loop back on itself. By using a de-duplication approach we only visit a
vertex exactly once and remove all other instances of it as the query progresses.
This gives the query processor a lot less work to do and as a result the query
executes more efficiently. The queries below do a bit less filtering than the ones
above but work well if all you are looking for are destinations that, from a given
starting point, require at least a specified number of hops to reach.

This time we decided to start with Austin (AUS) and look for any airports that are
only reachable in seven hops. The fact that we are removing duplicates along the way
guarantees that there is not a shorter path to the destinations found. If you are
good at reading backwards, you will see that the results match those found above but
in the reverse order.

The first example keeps track of where it has been using a 'aggregate' step and only
continues on to places it has not seen before until seven hops have completed. Note
that it is important to wrap 'aggregate' inside of 'local' to get lazy evaluation
behavior in this case. 

[source,groovy]
----
g.V().has('code','AUS').
      repeat(out().where(without('a')).local(aggregate('a'))).
        times(7).
      path().
        by('code')
----

As you can see the results look familiar.

[source,groovy]
----
[AUS,YYZ,YUL,YGL,YPX,AKV,YIK,YZG]
[AUS,YYZ,CPH,SFJ,JAV,JUV,NAQ,THU]
[AUS,YYZ,YTS,YMO,YFA,ZKE,YAT,YPO]
----

This alternate version of the query uses a 'dedup' step to achieve the same goal and
in a more performant manner. On most Gremlin query engines this is an efficient way 
to look for targets only reachable in at least the given number of hops. This query 
is similar to the one used in the "<<doesanyrouteexist>>" section.

[source,groovy]
----
g.V().has('code','AUS').
      repeat(out().dedup()).
        times(7).
      path().
        by('code')
----

Once again the same results are generated.

[source,groovy]
----
[AUS,PHL,YUL,YGL,YPX,AKV,YIK,YZG]
[AUS,PHL,KEF,GOH,JAV,JUV,NAQ,THU]
[AUS,YYZ,YTS,YMO,YFA,ZKE,YAT,YPO]
----

When looking for longest routes in a graph, especially in a highly connected one, you
need to be careful. Notice how we chose a very specific target or a specific number
of hops for our searches.

NOTE: Looking for longest paths must be done carefully as it can result in very long
running queries.

If you were to try and write a query that arbitrarily looked for the longest route
between any two airports you run the risk of writing a query that runs for an
extremely long time. In many ways the concept of "longest path" can be viewed as an
anti pattern. This is especially true in highly connected graphs of which
`air-routes` is an example.

As a final point about the prior example, we recall the "<<traversal-strategies>>" 
section, where we learned that TinkerPop applies some changes to the way a traversal
is written just before it is iterated to optimize performance where it can. The prior
example demonstrates a case where certain query patterns will prevent a particular
strategy from being applied. Normally, the 'repeat().times()' pattern will result in
the contents of 'repeat' being expanded, where 'repeat(out()).times(2)' becomes, 
'out().out()' which is more efficient for TinkerPop to execute. But the presence of 
'dedup()' precludes that transformation to get the query semantics that are 
presented. This sort of information can become useful when you are looking for your 
own query optimizations.

[[unwantededges]]
=== Finding unwanted parallel edges

In general terms there are many reasons in a graph that there could be more than one
edge between the same two vertices in the same direction. Most property graph
systems allow this and many data models take advantage of this capability. We call
these parallel edges. However, in the 'air-routes' graph we only model the existence
of a route using a single edge from airport A to airport B. It is considered an error
for there to be more than one edge between the same two airports in the same
direction. Note this does not include an edge going the other way (from B to A).

During development of the 'air-routes' graph, when we were still cleaning up the
data, we frequently ran into problems with parallel edges getting included in the
graph by mistake. we realized we could use Gremlin to help me detect these error
cases.

Initially we tried something very basic, that still required quite a bit of manual
reading of the output. We used the following query to tell me if for a given airport
there was more than one outgoing edge to any other airport. This required me to have
a hunch ahead of time which airport vertices might have an issue. Far from ideal when
there are over 3,300 airport vertices in the graph.

[source,groovy]
----
g.V().has('code','LHR').out().groupCount().by('code').
  order(local).by(values,desc).next().values().max()
----

If the answer came back greater than one we then ran the following query and manually
looked at each result to see where the duplicate edge was.

[source,groovy]
----
g.V().has('code','LHR').out().groupCount().by('code').order(local).by(values,desc)
----

As we said this was a very manual and time consuming process. We clearly needed a
better query. Given what we know about 'groupCount' we realized we could write an
arbitrary query to tell me how many times every single route in the graph exists.
However, given there are over 43,000 routes we were not going to be able to check
that manually. So as is often the best way with Gremlin we built up our query in
stages. First of all we wrote the query to count the occurrence of all routes. We
have not shown all the output from this query as it would take tens of pages but as
you can see from what we have shown, this result would still need a person studying
all of the results. Far from ideal!

[source,groovy]
----
g.V().as("a").out().as("b").groupCount().by(select("a","b"))

[[a:v[1],b:v[3]]:1,[a:v[1],b:v[6]]:1,[a:v[1],b:v[7]]:1 ...
----

We then added a filter to only select any routes that occurred more than once. Note
that we had to use 'unfold' before we applied the 'filter' to turn the map back into
a stream of values that could be filtered.

[source,groovy]
----
g.V().as("a").out().as("b").groupCount().by(select("a","b")).unfold().
                            filter(select(values).is(gt(1)))
----

Next we added one more step to tell me which routes contained the error.

[source,groovy]
----
g.V().as("a").out().as("b").groupCount().by(select("a","b")).unfold().
                            filter(select(values).is(gt(1))).select(keys)
----

One of the errors we ran into was that we had erroneously added the LHR to JFK route
twice into the graph. LHR has the ID of 49 and JFK has the ID of 12. When we ran the
above query we got the following output which told me exactly which route we needed
to correct. This is clearly a much more useful query than the prior ones. We could
happily have stopped at this point but we wanted to see if we could improve the query
some more.

[source,groovy]
----
[a:v[49],b:v[12]]
----

We were still not totally happy with our query as we really wanted it to give me back
the airport codes. So we added a few more tweaks to do the 'groupCount' using the
airport codes. we have also left the 'select(keys)' off the end of this query as we
think it aids understanding to see what is returned before that step is performed.

[source,groovy]
----
g.V().as("a").out().as("b").select('a','b').by('code').groupCount().unfold()
                           .filter(select(values).is(gt(1)))
----

So what we get back when we run that query is the airport codes as well of the number
of times they have appeared connected to each other by a parallel edge. This is
actually a key/value pair where the contents of the '{}' are the keys and the '=2'
part is the value. You could reasonably argue that this is sufficient for me to go
and fix the mistake in the graph. However we want to add just a couple of additional
steps to demonstrate other possible refinements that you may find useful in other
circumstances.

[source,groovy]
----
{a=LHR, b=JFK}=2
----

If we don't want the '=2' part returned we can just select the keys part.
[source,groovy]
----
g.V().as("a").out().as("b").select('a','b').by('code').groupCount().unfold()
                           .filter(select(values).is(gt(1))).select(keys)
----

This is what was now returned. Note that the keys themselves are returned in a map
using the 'a' and 'b' terms from our 'as' step.

[source,groovy]
----
[a:LHR,b:JFK]
----

This is almost exactly what we wanted but we can add one more tiny step to clean up
the output by just selecting the values from the map returned.

[source,groovy]
----
g.V().as("a").out().as("b").
  select('a','b').by('code').
  groupCount().unfold().
  filter(select(values).is(gt(1))).
  select(keys).select(values)
----

So here is the final output, just the codes for the airports with parallel edges
that needed fixing.

[source,groovy]
----
[LHR,JFK]
----

TIP: Note how we built up our Gremlin query in stages to solve this problem. We
recommend this as a sensible way to approach all but the most basic of queries that
you may need to write. Doing it this way has the advantage that you can also check
that each part of the query is working the way you intend it to before you add more
parts to it.

[[groupcountpath]]
==== Using 'groupCount' with 'path' to find duplicate edges

The prior example found duplicate edges by working with a map created using 'select'
and 'groupCount' steps. It is also possible to use a 'path' step inside a
'groupCount' step to achieve a similar result.

First of all let's create a duplicate edge between Austin (AUS) and Cancun (CUN).

[source,groovy]
----
g.V().has('code','AUS').addE('route').to(V().has('code','CUN'))

e[53791][3-route->180]
----

We can now use a 'groupCount' step containing a 'path' step to look for duplicate
edges originating in Austin. We used a 'limit' step just to reduce the amount of
output a bit.

[source,groovy]
----
g.V().has('code','AUS').out().
      groupCount().by(path().by('code')).limit(local,5)
----

As you can see from the results below the route between AUS and CUN has a count of 2
associated with it.

[source,groovy]
----
[[AUS,CUN]:2,[AUS,MDW]:1,[AUS,MIA]:1,[AUS,DFW]:1,[AUS,BWI]:1]
----

Note that this technique does not replace the full query shown in the prior section.
It is more intended to show that you can place a 'path' step inside of a 'groupCount'
step and achieve some useful results.

[[flr]]
=== Finding the longest flight route between two adjacent airports in the graph

This section is in a way a case study in the good and the bad of the Gremlin query
language. We have documented here the learning steps we went through to get what we
thought was a very simple question expressed as a single Gremlin query. This
documents well the Good,the bad and the sometimes ugly aspects of working with
Gremlin. On the good side it is powerful and some things that should be easy are
easy. On the bad side, some things that appear easy are in fact very hard to get
right especially if you want to build a single query to do a job rather than break it
up into a set of smaller programmatic steps. Let's look at an example of this with a
real-world query.

The query we want to build is to find the route(s) in the graph that are of the
maximum length between any two airports. It turns out that this simple looking query
takes a lot of work to get right if you want to handle the case where there could be
more than one route that is of the maximum length (return flights between the same
two airports don't count as different routes for this example).

While using a 'max' step, as shown below, might seem like the obvious and easy to do
what we need, because of the way that 'max' is implemented it will not work.
Currently the 'max' and 'min' steps cause the prior paths that have been taken in a
traversal to be lost. We need to explore other ways of achieving our desired result.

[source,groovy]
----
g.E().hasLabel('route').as('e').values('dist').max().select('e')
----

First of all we could do this (below) The downside of this approach is that both
queries have to look at a lot of edges which is likely to use additional memory and
CPU to process.

[source,groovy]
----
r=g.E().hasLabel('route').values('dist').max().next()
g.E().has('dist',r).bothV().values('code')
----

This next query is more efficient as using an ID means the edges are only searched
once, but this approach would miss the case where more than one route was of the same
(max) length so it does not meet our required success criteria.

[source,groovy]
----
r=g.E().hasLabel('route').as('e').order().by('dist', desc).
    limit(1).select('e').id().next()
g.E(r).values('dist')
g.E(r).bothV().values('code')
----

This query is what Daniel Kuppitz from the TinkerPop team recommended after we
discussed this on the mailing list and it does indeed work but from where we started
our experiments to build up this query from there is a non trivial journey!

[source,groovy]
----
g.E().hasLabel("route").order().by("dist", desc).local(aggregate("d").by("dist")).
  filter(values("dist").as("cd").
         select("d").by(limit(local, 1).unfold()).as("md").
         where("cd",eq("md"))).
  project("from","to","dist").
    by(outV()).
    by(inV()).by("dist")
----

Finally, to output airport codes rather than just vertices, we can tweak the query
one more time as follows.

[source,groovy]
----
g.E().hasLabel("route").order().by("dist",desc).local(aggregate("d").by("dist")).
  filter(values("dist").as("cd").
         select("d").by(limit(local, 1).unfold()).as("md").
         where("cd",eq("md"))).
  project("from","to","dist").
    by(outV().values('code')).
    by(inV().values('code')).
    by("dist")
----

Now we are ready to run our query. This is the output from it. Notice how both routes
are between the same city pairs. We could have further refined our query to only show
such combinations once but we will leave that as an exercise for the reader!

[source,groovy]
----
[from:SIN,to:JFK,dist:9526]
[from:JFK,to:SIN,dist:9526]
----

[[miscq]]
=== Miscellaneous other queries

The examples in this section demonstrate a few miscellaneous features and queries
that we have not yet had a chance to examine. Over time, these queries should
probably be moved to other sections of the book.

==== Using a calculation inside of an 'is' step

It is possible to use a mathematical expression inside of an 'is' step. At times this
comes in quite handy. The example below simply divides 500 by 2 as part of a test.
Clearly you would normally just enter 250 but this shows the capability.

[source,groovy]
----
g.V().hasLabel('airport').where(out().count().is(gt(500/2))).values('code')

DFW
ORD
CDG
FRA
AMS
MUC
IST
----

The capability becomes more interesting when used in conjunction with a variable.

[source,groovy]
----
a = 500

g.V().hasLabel('airport').where(out().count().is(gt(a/2))).values('code')

DFW
ORD
CDG
FRA
AMS
MUC
IST
----

Here is one more example where we start by setting the variable 'a' to the value that
represents the maximum number of routes from any single airport. The we use a query
to find all the airports that have at least as many outgoing routes as 50 fewer than
our value 'a'.

[source,groovy]
----
a = g.V().local(out('route').count()).max().next()

g.V().hasLabel('airport').where(out().count().is(gt(a-50))).
      project('apt','routes').by('code').by(out().count())

[apt:ORD,routes:265]
[apt:CDG,routes:293]
[apt:FRA,routes:310]
[apt:AMS,routes:283]
[apt:MUC,routes:270]
[apt:IST,routes:309]
----
